% Special-Operator TAGBODY

[UP](5.3.html)  

---

# Special-Operator TAGBODY


Special Operator `TAGBODY`


## 構文

`tagbody` `{`*tag* | *statement*`}`\* => `nil`


## 引数と戻り値

*tag* - `go`のタグ、評価されません。  
*statement* - compound-form、下記の説明通り評価されます。


## 定義

*tag*によってラベル付けされた場所へ制御を移す機能を提供するための
レキシカル環境下において、0個か複数の*statement*を実行します。

[`tagbody`](5.3.tagbody.html)の*statement*は、左から右の順番で評価され、
それらの値は捨てられます。
もしどんなときでも、残りの*statement*がない場合は、
[`tagbody`](5.3.tagbody.html)は`nil`を返却します。
しかし、もし`(go tag)`が評価されたときは、
制御が*tag*でラベル付けされたボディ部の部分にジャンプします
（タグは`eql`によって比較されます）。

[`tagbody`](5.3.tagbody.html)で確立した*tag*は、レキシカルスコープと動的エクステントを持ちます。
いったん[`tagbody`](5.3.tagbody.html)を終えると、
そのボディの*tag*に対する[`go`](5.3.go.html)はもはや有効ではありません。
[`go`](5.3.go.html)のジャンプは、
その[`go`](5.3.go.html)が含まれるもっとも内側の[`tagbody`](5.3.tagbody.html)以外への
ものも許されます。
[`tagbody`](5.3.tagbody.html)の*tag*の確立は、
ただ同じ名前の他のタグをシャドウします。

ボディのどの要素が*tagでどの要素が*statement*であるかの判断は、
その要素のマクロ展開の前に行われます。
もし*statement*がマクロフォームで、
そのマクロ展開がアトムである場合、
そのアトムは*tag*ではなく*statement*として扱われます。


Examples:

```lisp
(let (val)
   (tagbody
     (setq val 1)
     (go point-a)
     (incf val 16)
    point-c
     (incf val 04)
     (go point-b)
     (incf val 32)
    point-a
     (incf val 02)
     (go point-c)
     (incf val 64)
    point-b
     (incf val 08))
   val)
=>  15
(defun f1 (flag)
  (let ((n 1))
    (tagbody 
      (setq n (f2 flag #'(lambda () (go out))))
     out
      (prin1 n))))
=>  F1
(defun f2 (flag escape)
  (if flag (funcall escape) 2))
=>  F2
(f1 nil)
>>  2
=>  NIL
(f1 t)
>>  1
=>  NIL
```


## 影響

なし。


## 例外

なし。


## 参考

[`go`](5.3.go.html)


## 備考

次の図のマクロは、暗黙の`tagbody`を持ちます。


```
do              do-external-symbols  dotimes  
do*             do-symbols           prog     
do-all-symbols  dolist               prog*    
```

Figure 5-10. 暗黙の`tagbody`をもつマクロ


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

