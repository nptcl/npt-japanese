% Macro CASE, CCASE, ECASE

[UP](5.3.html)  

---

# Macro **CASE, CCASE, ECASE**


Macro `CASE`, `CCASE`, `ECASE`


## 構文

`case` *keyform* `{`*normal-clause*`}`\* [*otherwise-clause*] => *result\**  
`ccase` *keyplace* `{`*normal-clause*`}`\* => *result\**  
`ecase` *keyform* `{`*normal-clause*`}`\* => *result\**

```
normal-clause ::= (keys form*) 
otherwise-clause ::= ({otherwise | t} form*) 
clause ::= normal-clause | otherwise-clause 
```


## 引数と戻り値

*keyform* - フォーム。評価され*test-key*を返却します。  
*keyplace* - フォーム。評価されて最初に*test-key*を返却します。  
もしキーにマッチしなかったときは、可能であれば後で`place`としても使われます。  
*test-key* - *keyform*か*keyplace*が評価されることによって返却されるオブジェクト。  
*keys* - オブジェクトのリストの指定子。
[`case`](5.3.case.html)の場合、シンボル[`t`](5.3.t-variable.html)と[`otherwise`](5.3.case.html)は
キー指定子として使用することができません。
キーとしてそれらのシンボルを使用したいときは、
それぞれ`(t)`、`(otherwise)`をかわりに使わなければいけません。  
*form* - 暗黙のprogn  
*result* - マッチした*clause*の*form*の返却値


## 定義

これらのマクロは、*test-key*とのマッチによって選択された
*clause*内の*form*のボディを条件付きで実行します。

*keyform*と*keyplace*が評価されて、*test-key*が生成されます。

そのあと各*normal-clause*が順番に考慮されます。
もし*test-key*が*clause*のどれかのキーと同じであれば、
その*clause*内の*form*が暗黙のprognとして評価され、
[`case`](5.3.case.html)、[`ccase`](5.3.case.html)、[`ecase`](5.3.case.html)フォームの返却値として
その多値が返却されます。

これらのマクロは、もし*normal-clause*がマッチしなかったときにだけ、
動作が違っています。
違いを下記に示します。

- [`case`](5.3.case.html)
  - もしマッチする*normal-clause*が存在せず*otherwise-clause*があるときは、
    自動的に*otherwise-clause*がマッチします。
    その*clause*にある*form*は暗黙のprognとして評価され、
    その*clause*内の*form*は暗黙のprognとして評価され、
    [`case`](5.3.case.html)フォームの返却値としてその多値が返却されます。
    <br><br>
    もし*otherwise-clause*がないときは、
    [`case`](5.3.case.html)は[`nil`](5.3.nil-variable.html)を返却します。
    <br><br>

- [`ccase`](5.3.case.html)
  - もしマッチする*normal-clause*が存在しないときは、
    型`type-error`の修正可能なエラーが発生します。
    問題の`datum`は*test-key*であり、
    `expected type`は`(member key1 key2 ...)`と同等の型です。
    `store-value` `restart`をエラーの修正に使うことができます。
    <br><br>
    もし`store-value` `restart`が起動されたら、
    その引数は新しい*test-key*であり、
    *keyplace*への保存は`(setf keyplace test-key)`のように行われます。
    それから[`ccase`](5.3.case.html)は最初から始められ、
    各*clause*が新しく評価されます。
    <br><br>
    いずれのケースも成立しない場合、
    *keyplace*のサブフォームは再度評価されるかもしれません。
    <br><br>

- [`ecase`](5.3.case.html)
  - もしマッチする*normal-clause*が存在しないときは、
    型`type-error`の修正不可能なエラーが発生します。
    問題の`datum`は*test-key*であり、
    `expected type`は`(member key1 key2 ...)`と同等の型です。
    <br><br>
    [`ccase`](5.3.case.html)とは対照的に[`ecase`](5.3.case.html)の使用者は、
    もし*normal-clause*がマッチしなかったら
    [`ecase`](5.3.case.html)は戻ってこないという事実に頼っています。
    <br><br>


## 例文

```lisp
(dolist (k '(1 2 3 :four #\v () t 'other))
   (format t "~S "
      (case k ((1 2) 'clause1)
              (3 'clause2)
              (nil 'no-keys-so-never-seen)
              ((nil) 'nilslot)
              ((:four #\v) 'clause4)
              ((t) 'tslot)
              (otherwise 'others)))) 
>>  CLAUSE1 CLAUSE1 CLAUSE2 CLAUSE4 CLAUSE4 NILSLOT TSLOT OTHERS 
=>  NIL
(defun add-em (x) (apply #'+ (mapcar #'decode x)))
=>  ADD-EM
(defun decode (x)
  (ccase x
    ((i uno) 1)
    ((ii dos) 2)
    ((iii tres) 3)
    ((iv cuatro) 4)))
=>  DECODE
(add-em '(uno iii)) =>  4
(add-em '(uno iiii))
>>  Error: The value of X, IIII, is not I, UNO, II, DOS, III,
>>         TRES, IV, or CUATRO.
>>   1: Supply a value to use instead.
>>   2: Return to Lisp Toplevel.
>>  Debug> :CONTINUE 1
>>  Value to evaluate and use for X: 'IV
=>  5
```


## 副作用

デバッガーに入るかもしれません。
もし`store-value` `restart`が実行されたとき、
*keyplace*の値は変更されるかもしれません。


## 影響

[`ccase`](5.3.case.html)と[`ecase`](5.3.case.html)がエラーを通知したときは、
既存の`handler`と`*debug-io*`に影響を及ぼす可能性があります。


## 例外

*normal-clause*にマッチしなかったときは、
[`ccase`](5.3.case.html)と[`ecase`](5.3.case.html)は
型`type-error`のエラーが発生します。


## 参考

[`cond`](5.3.cond.html),
[`typecase`](5.3.typecase.html),
[`setf`](5.3.setf.html),
5.1. 一般化された参照


## 備考

```lisp
(case test-key
  {((key*) form*)}*)
== 
(let ((#1=#:g0001 test-key))
  (cond {((member #1# '(key*)) form*)}*))
```

[`ecase`](5.3.case.html)と[`ccase`](5.3.case.html)によって使われる特定のエラーメッセージは、
実装によって変わります。
そんな中で、エラーメッセージの特定の単語の制御が必要なときは、
[`case`](5.3.case.html)の*otherwise-clause*で
明示的に適切なエラーメッセージを発生させるのが良いでしょう。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

