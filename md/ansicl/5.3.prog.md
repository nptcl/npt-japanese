% Macro PROG, PROG\*

[UP](5.3.html)  

---

# Macro **PROG, PROG\***


Macro `PROG`, `PROG*`


## 構文

`prog` (`{`*var* `|` (*var* [*init-form*])`}`\*)
 *declaration\** `{`*tag* `|` *statement*`}`\*  
=> *result\**

`prog*` (`{`*var* `|` (*var* [*init-form*])`}`\*)
 *declaration\** `{`*tag* `|` *statement*`}`\*  
=> *result\**


## 引数と戻り値

*var* - 変数名  
*init-form* - フォーム  
*declaration* - 宣言式。評価されません。  
*tag* - `go`タグ。評価されません。  
*statement* - compound-form。下記の説明に従って評価されます。  
*result* - 通常の返却のときは[`nil`](5.3.nil-variable.html)、
そうではなく`return-from`による明示的な返却のときは転送された返却値。


## 定義

[`prog`](5.3.prog.html)と[`prog*`](5.3.prog.html)によって
3つの異なった操作が実行されます。
それは、ローカル変数の束縛と、
[`return`](5.3.return.html)構文の使用の許可と、
[`go`](5.3.go.html)構文の使用の許可です。
典型的な[`prog`](5.3.prog.html)の見た目は次のようになります。

```lisp
(prog (var1 var2 (var3 init-form-3) var4 (var5 init-form-5))
      declaration*
      statement1
 tag1
      statement2
      statement3
      statement4
 tag2
      statement5
      ...
      )
```

[`prog`](5.3.prog.html)では、
最初に各*init-form*が指定された順に評価されます。
そのあと*var*が対応する値と並列に束縛されます。
もし*var*に対して*init-form*が存在しないとき、
その*var*には[`nil`](5.3.nil-variable.html)が束縛されます。

[`prog`](5.3.prog.html)のボディ部は
それが[`tagbody`](5.3.tagbody.html)フォームであるかのように実行されます。
[`go`](5.3.go.html)構文は*tag*へ制御を移すときに使うことができます。
*tag*は*statement*にラベル付けされます。

[`prog`](5.3.prog.html)は暗黙に、[`prog`](5.3.prog.html)フォーム全体を囲うようにして、
[`nil`](5.3.nil-variable.html)という[`block`](5.3.block.html)が確立しており、
[`return`](5.3.return.html)を使って[`prog`](5.3.prog.html)フォームからいつでも退出できます。

[`prog*`](5.3.prog.html)と[`prog`](5.3.prog.html)の違いは、
[`prog*`](5.3.prog.html)は*var*の束縛と初期化は連続的に行われ、
各変数の*init-form*の値は、以前の変数の値によって使用することができます。


## 例文

```lisp
(prog* ((y z) (x (car y)))
       (return x))
```

上記は`z`の値の`car`を返却します。


```lisp
(setq a 1) =>  1
(prog ((a 2) (b a)) (return (if (= a b) '= '/=))) =>  /=
(prog* ((a 2) (b a)) (return (if (= a b) '= '/=))) =>  =
(prog () 'no-return-value) =>  NIL
(defun king-of-confusion (w)
  "Take a cons of two lists and make a list of conses.
   Think of this function as being like a zipper."
  (prog (x y z)          ;Initialize x, y, z to NIL
       (setq y (car w) z (cdr w))
   loop
       (cond ((null y) (return x))
             ((null z) (go err)))
   rejoin
       (setq x (cons (cons (car y) (car z)) x))
       (setq y (cdr y) z (cdr z))
       (go loop)
   err
       (cerror "Will self-pair extraneous items"
               "Mismatch - gleep!  ~S" y)
       (setq z y)
       (go rejoin))) =>  KING-OF-CONFUSION 
```

これは下記に示すように、より明確に達成できます。

```lisp
(defun prince-of-clarity (w)
  "Take a cons of two lists and make a list of conses.
   Think of this function as being like a zipper."
  (do ((y (car w) (cdr y))
       (z (cdr w) (cdr z))
       (x '() (cons (cons (car y) (car z)) x)))
      ((null y) x)
    (when (null z)
      (cerror "Will self-pair extraneous items"
             "Mismatch - gleep!  ~S" y)
      (setq z y)))) =>  PRINCE-OF-CLARITY 
```


## 影響

なし。


## 例外

なし。


## 参考

[`block`](5.3.block.html),
[`let`](5.3.let.html),
[`tagbody`](5.3.tagbody.html),
[`go`](5.3.go.html),
[`return`](5.3.return.html),
3.1. 評価


## 備考

[`prog`](5.3.prog.html)は、[`block`](5.3.block.html)と[`let`](5.3.let.html)と[`tagbody`](5.3.tagbody.html)の語を用いて
下記のように説明することができます。

```lisp
(prog variable-list declaration . body)
   ==  (block nil (let variable-list declaration (tagbody . body)))
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

