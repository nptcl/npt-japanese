% Accessor SYMBOL-FUNCTION

[UP](10.2.html)  

---

# Accessor **SYMBOL-FUNCTION**


Accessor `SYMBOL-FUNCTION`


## 構文

`symbol-function` *symbol* => *contents*  
(`setf` (`symbol-function` *symbol*) *new-contents*)


## 引数と戻り値

*symbol* - シンボル  
*contents* - もし*symbol*がマクロか特殊オペレーターとして
グローバルに宣言されているものならば、
実装依存の性質と固有値であるオブジェクトが返却されます。
もし*symbol*がマクロか特殊オペレーターとしてグローバルに
宣言されていないのであれば、
その*symbol*は`fbound`であり、関数オブジェクトが返却されます。  
*new-contents* - 関数


## 定義

*symbol*の関数セルにアクセスします。


## 例文

```lisp
(symbol-function 'car) =>  #<FUNCTION CAR>
(symbol-function 'twice) はエラー   ;なぜならTWICEは定義されていない
(defun twice (n) (* n 2)) =>  TWICE
(symbol-function 'twice) =>  #<FUNCTION TWICE>
(list (twice 3)
      (funcall (function twice) 3)
      (funcall (symbol-function 'twice) 3))
=>  (6 6 6)
(flet ((twice (x) (list x x)))
  (list (twice 3)
        (funcall (function twice) 3)
        (funcall (symbol-function 'twice) 3)))
=>  ((3 3) (3 3) 6)   
(setf (symbol-function 'twice) #'(lambda (x) (list x x)))
=>  #<FUNCTION anonymous>
(list (twice 3)
      (funcall (function twice) 3)
      (funcall (symbol-function 'twice) 3))
=>  ((3 3) (3 3) (3 3))
(fboundp 'defun) =>  true
(symbol-function 'defun)
=>  implementation-dependent
(functionp (symbol-function 'defun))
=>  implementation-dependent
(defun symbol-function-or-nil (symbol)
  (if (and (fboundp symbol) 
           (not (macro-function symbol))
           (not (special-operator-p symbol)))
      (symbol-function symbol)
      nil)) =>  SYMBOL-FUNCTION-OR-NIL
(symbol-function-or-nil 'car) =>  #<FUNCTION CAR>
(symbol-function-or-nil 'defun) =>  NIL
```


## 副作用

なし。


## 影響

[`defun`](5.3.defun.html)


## 例外

*symbol*がシンボルではないとき、
型`type-error`のエラーが発生します。

もし*symbol*が`fbound`ではなく、
その定義を読み込もうとしたとき、
[`undefined-function`](5.3.undefined-function.html)が通知されます。
（その定義を書き込もうとしたときに発生するエラーはありません）


## 参考

[`fboundp`](5.3.fboundp.html),
[`fmakunbound`](5.3.fmakunbound.html),
`macro-function`,
`special-operator-p`


## 備考

[`symbol-function`](10.2.symbol-function.html)は、
[`flet`](5.3.flet.html)か[`labels`](5.3.flet.html)によって生成された
レキシカルな関数の値にアクセスできません。
それはただグローバルな関数の値にのみアクセスできます。

[`setf`](5.3.setf.html)は、[`symbol-function`](10.2.symbol-function.html)とともに使用することで、
*symbol*の関数定義が特殊オペレーターとして表現されていない場合は、
グローバルな関数定義を置き換えます。

```lisp
(symbol-function symbol) ==  (fdefinition symbol)
```

しかし、[`fdefinition`](5.3.fdefinition.html)は、引数にシンボル以外を受け付けます。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

