;;
;;  ANSI Common Lisp 日本語訳
;;  7. オブジェクト
;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  Standard Generic Function MAKE-LOAD-FORM
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@begin: 7.7.make-load-form
@name: make-load-form
@type: standard-generic-function

Standard Generic Function `MAKE-LOAD-FORM`


## {syntax}

`make-load-form` *object* &optional *environment*
=> *creation-form*[, *initialization-form*]


## {method-signatures}

`make-load-form` (*object* {standard-object:link}) &optional *environment*  
`make-load-form` (*object* {structure-object:link}) &optional *environment*  
`make-load-form` (*object* {condition:link}) &optional *environment*  
`make-load-form` (*object* {class:link}) &optional *environment*


## {arguments-and-values}

object - オブジェクト  
environment - 環境オブジェクト  
creation-form - フォーム  
initialization-form - フォーム


## {description}

ジェネリック関数{make-load-form:link}は、
ひとつか2つのフォームを返却します。
それぞれ*creation-form*と*initialization-form*であり、
{load:link}時に*object*と等価なオブジェクトを構築することができます。
*environment*は、フォームが処理されるときの
{lexical-environment}に対応する環境オブジェクトです。

ファイルコンパイラは、特定のクラスの{literal-object}を処理するため、
{make-load-form:link}を呼び出します。
{3.2.4.4:ab}を参照。

{confirming-program}は、*object*が{standard-object:link},
{structure-object:link}, {condition:link}からなる一般的なインスタンスであれば、
{make-load-form:link}を直接呼び出します。

*creation-form*は{load:link}時に評価されるフォームであり、
*object*と等価なオブジェクトを返却しなければなりません。
等価の正確な意味は*object*の型によって異なるので、
{make-load-form:link}のメソッドを定義するプログラマーに任されています。
{3.2.4:ab}を参照。

もし*object*が*initialization-form*に定数として現れたときは、
ロード時にそれは、*creation-form*によって生成されたオブジェクトと
同一なものとして置き換えられます。
これは、さらなる初期化処理でオブジェクトにアクセスする方法です。

*creation-form*と*initialization-form*の両方で、
外部オブジェクトの参照が含まれる場合があります。
しかし、*creation-forms*では循環的な依存関係はがあってはなりません。
循環的な依存関係とは、例えば
オブジェクト`X`の*creation-form*にオブジェクト`Y`が含まれており、
オブジェクト`Y`の*creation-form*にオブジェクト`X`が
含まれているような場合です。
*initialization-form*には循環的な依存関係のような制約はありません。
これが*initialization-form*フォームが存在する理由です。
循環的なデータ構造の例は下記を参照してください。

*object*の*creation-form*は、常に*initialization-form*より前に実行されます。
*creation-form*または*initialization-form*のいずれかが、
コンパイル中のファイル内で
以前に参照されていない他のオブジェクトを参照する場合、
コンパイラは、その参照しているフォームを評価する前に、
すべての参照されているオブジェクトが作成されたことを確認します。
参照されるオブジェクトが、ファイルコンパイラによって
{make-load-form:link}を実行するタイプの場合は、
そのオブジェクトのために、
評価された*creation-form*の返却値を実行します。
（これが、*creation-form*で循環参照を禁止している理由です。）

各*initialization-form*は、データフローに従って、
その*creation-form*実行の後、できるだけ早く評価されます。
もしファイル内で、*object*の*initialization-form*が、
まだ参照されていない他のどんなオブジェクトも参照していないときに
そのファイルコンパイラが{make-load-form:link}を実行した場合は、
*initialization-form*は*creation-form*処理直後に実行されます。

もし*creation-form*か*initialization-form*のフォームを`F`としたとき、
`F`がそのようなオブジェクトを参照を含む場合は、
それらのオブジェクトは*creation-form*は`F`の前に実行され、
また、それらが`F`によって生成と初期化が行われるオブジェクトに依存しない限り、
それらのオブジェクトの*initialization-form*も`F`の前に実行されます。
これらの規則によって、作成と初期化の2フォーム間で
評価する順序が一意に決定されない個所については、
その評価順序は決められていません。

これらの作成と初期化のフォームが評価されている間の
オブジェクトはおそらく初期化されていない状態であり、
例えるなら、{allocate-instance:link}で作成され、
{initialize-instance:link}で完全に初期化処理されるまでの
オブジェクトの状態と似ています。
{make-load-form:link}のメソッドを作成するプログラマーは、
まだ初期化されていないスロットに依存しないよう、
注意深くオブジェクトを操作しなければなりません。

{load:link}がフォームを{eval:link}で処理するか、
あるいは同等の効果を持つ他のオペレーションを行うかは、
処理系に依存します。
例をあげると、フォームを別の違った同等の形式に変換して評価する、
それらをコンパイルした結果の関数を{load:link}から呼ぶ、
それらが{eval:link}とは違う特殊な目的の関数によって実行される、
などがあります。
フォームを評価するということと同等の効果があればよいのです。

型{class:link}に特化したメソッドは、
もしクラスがちゃんとした名前を持っているときは、
クラス名を使った*creation-form*が返却されます。
もしクラスが名前を持っていない場合は、
型{error!condition:link}のエラーが発せられます。
*creation-form*の評価は、{find-class:link}を呼び出すように、
その名前を使用してクラスを検索します。
もしその名前のクラスが定義されていないときは、
クラスは実装で定義した方法で処理されます。
もし*creation-form*を評価した結果、
クラスを返却できなかった場合は、
型{error!condition:link}のエラーが発生します。

{confirming-implementation}と{confirming-program}の両方が
{make-load-form:link}をさらに特別化することができます。


## {examples}

```lisp
(defclass obj ()
   ((x :initarg :x :reader obj-x)
    (y :initarg :y :reader obj-y)
    (dist :accessor obj-dist)))
=>  #<STANDARD-CLASS OBJ 250020030>
(defmethod shared-initialize :after ((self obj) slot-names &rest keys)
  (declare (ignore slot-names keys))
  (unless (slot-boundp self 'dist)
    (setf (obj-dist self)
          (sqrt (+ (expt (obj-x self) 2) (expt (obj-y self) 2))))))
=>  #<STANDARD-METHOD SHARED-INITIALIZE (:AFTER) (OBJ T) 26266714>
(defmethod make-load-form ((self obj) &optional environment)
  (declare (ignore environment))
  ;; この定義は、XとYがオブジェクト自身を参照する情報を
  ;; 含んでいない場合にのみ有効であることに注意してください。
  ;; この問題に対するより一般的な解決策については、
  ;; 以下の改訂例を参照してください。
  `(make-instance ',(class-of self)
                  :x ',(obj-x self) :y ',(obj-y self)))
=>  #<STANDARD-METHOD MAKE-LOAD-FORM (OBJ) 26267532>
(setq obj1 (make-instance 'obj :x 3.0 :y 4.0)) =>  #<OBJ 26274136>
(obj-dist obj1) =>  5.0
(make-load-form obj1) =>  (MAKE-INSTANCE 'OBJ :X '3.0 :Y '4.0)
```

上記の例では、`obj`の等価なインスタンスは、
そのスロットのうちの2つの値を使用して再構築されます。
3番目のスロットの値は、これら2つの値から導かれます。

{make-load-form:link}メソッドの別の書き方の例は、
{make-load-form-saving-slots:link}を使うことです。
このメソッドが生成するコードは、
上に示した{make-load-form:link}メソッドとは
少し異なる結果をもたらすかもしれませんが、
操作上の効果は同じです。
例を示します。

```lisp
;; 上記で定義したメソッドを再定義する
(defmethod make-load-form ((self obj) &optional environment)
   (make-load-form-saving-slots self
                                :slot-names '(x y)
                                :environment environment))
=>  #<STANDARD-METHOD MAKE-LOAD-FORM (OBJ) 42755655>

;; 上記で作成したオブジェクトにMAKE-LOAD-FORMを実行
(make-load-form obj1)
=>  (ALLOCATE-INSTANCE '#<STANDARD-CLASS OBJ 250020030>),
   (PROGN
     (SETF (SLOT-VALUE '#<OBJ 26274136> 'X) '3.0)
     (SETF (SLOT-VALUE '#<OBJ 26274136> 'Y) '4.0)
     (INITIALIZE-INSTANCE '#<OBJ 26274136>))
```

次の例は、`my-frob`のインスタンスが、
何らかの方法で**intern**されています。
スロット`name`の値をキーとして既存のオブジェクトを検索し、
同等のインスタンスを再構築しています。
この例では、プログラマーはオブジェクトが存在しなかったとき、
新しいをオブジェクトを生成することを選んでいますが、
他にはエラーを発生させるという場合もあります。

```lisp
(defclass my-frob ()
   ((name :initarg :name :reader my-name)))
(defmethod make-load-form ((self my-frob) &optional environment)
  (declare (ignore environment))
  `(find-my-frob ',(my-name self) :if-does-not-exist :create))
```

次の例では、各親がその子のリストを持ち、
各子がその親に戻る参照を持っているので、
データ構造は循環しています。
このような構造のひとつのオブジェクトに{make-load-form:link}が呼ばれると、
*creation-form*は同等のオブジェクトを作成し、
`children`スロットを埋め、
その子や孫などすべてのオブジェクトに対して
同等のオブジェクトを強制的に作成します。
この時点では、`parent`スロットはひとつも埋まっていません。
*initialization-form*フォームでは、
`parent`スロットが埋められ、
親に相当するオブジェクトがまだ作成されていない場合は、
強制的に作成されます。
したがって、ロード時にツリー全体が再作成されます。
コンパイル時では、
{make-load-form:link}はツリー内の各オブジェクトに対して
一度だけ呼び出されます。
すべての*creation-form*が処理系に依存した順序で評価され、
次にすべての*initialization-form*が、
同じく処理系に依存した順序で評価されます。

```lisp
(defclass tree-with-parent () ((parent :accessor tree-parent)
                               (children :initarg :children)))
(defmethod make-load-form ((x tree-with-parent) &optional environment)
  (declare (ignore environment))
  (values
    ;; creation form
    `(make-instance ',(class-of x) :children ',(slot-value x 'children))
    ;; initialization form
    `(setf (tree-parent ',x) ',(slot-value x 'parent))))
```

次のの例では、データ構造に特別な特性はなく出力され、
単純にスロットの内容を再構築するだけで
同等の構造体を再構築することができます。

```lisp
(defstruct my-struct a b c)
(defmethod make-load-form ((s my-struct) &optional environment)
   (make-load-form-saving-slots s :environment environment))
```


## {affected-by}

なし。


## {exceptional-situations}

{standard-object:link}, {structure-object:link}, {condition:link}
の特定メソッドは、型{error!condition:link}のエラーが発生します。

システムクラスの一般化されたインスタンスに対して{make-load-form:link}を呼び出すと、
エラーが発生するか、作成・初期化フォームが返されるかは、処理系に依存します。


## {see-also}

{compile-file:link},
{make-load-form-saving-slots:link},
{3.2.4.4:ab},
{3.1:ab},
{3.2:ab}


## {notes}

ファイルコンパイラは、特定の状況下で{make-load-form:link}を呼び出します。
{3.2.4.4:ab}を参照。

実装によっては、システムクラスとして指定されたクラスの新しいサブクラスを
定義する機能を提供する場合があります。
（考えられる候補としては、
{generic-function:link}、{method:link}、{stream:link}があります。）
そのような実装では、ファイルコンパイラがそのようなクラスのインスタンスを
{literal-object}として遭遇したときにどのように処理するかを文書化し、
{make-load-form:link}に関連するメソッドを文書化しなければなりません。

@end

