% Function DELETE-PACKAGE

[UP](11.2.html)  

---

# Function **DELETE-PACKAGE**


Function `DELETE-PACKAGE`


## 構文

`delete-package` *package* => *generalized-boolean*


## 引数と戻り値

*package* - パッケージ指定子  
*generalized-boolean* - generalized-boolean


## 定義

[`delete-package`](11.2.delete-package.html)は*package*を
全てのパッケージシステムのデータ構造から削除します。
もし操作が成功したとき、[`delete-package`](11.2.delete-package.html)は*true*を、
それ以外は[`nil`](5.3.nil-variable.html)を返却します。
[`delete-package`](11.2.delete-package.html)の効果は、
*package*の名前とニックネームが
認識されたパッケージの名前ではなくなるというものです。
そのパッケージオブジェクトは、まだパッケージです
（例えば`packagep`は*true*になります）が、
`package-name`は[`nil`](5.3.nil-variable.html)を返却します。
`COMMON-LISP`パッケージか`KEYWORD`パッケージを削除した結果は未定義です。
いったん削除された*package*に対して
他のパッケージの操作を行った結果は未定義です。
とくに削除された*package*を[`*package*`](11.2.package-variable.html)に束縛したときか、
あるいは削除された*package*を引数に指定したときに、
[`find-symbol`](11.2.find-symbol.html)や`intern`や、
パッケージからシンボル名を探すような他の関数によって
実行されたときの結果は未定義です。

もし*package*がすでに削除されたパッケージオブジェクトであったときは、
[`delete-package`](11.2.delete-package.html)は即座に[`nil`](5.3.nil-variable.html)を返却します。

この操作が完了したあと、
前のホームパッケージが*package*であったシンボルの
ホームパッケージは実装依存です。
例外として、*package*内のアクセス可能なシンボルは、
ホームパッケージが*package*ではないシンボルは変更されません。


## 例文

```lisp
(setq *foo-package* (make-package "FOO" :use nil))
(setq *foo-symbol*  (intern "FOO" *foo-package*))
(export *foo-symbol* *foo-package*)

(setq *bar-package* (make-package "BAR" :use '("FOO")))
(setq *bar-symbol*  (intern "BAR" *bar-package*))
(export *foo-symbol* *bar-package*)
(export *bar-symbol* *bar-package*)

(setq *baz-package* (make-package "BAZ" :use '("BAR")))

(symbol-package *foo-symbol*) =>  #<PACKAGE "FOO">
(symbol-package *bar-symbol*) =>  #<PACKAGE "BAR">

(prin1-to-string *foo-symbol*) =>  "FOO:FOO"
(prin1-to-string *bar-symbol*) =>  "BAR:BAR"

(find-symbol "FOO" *bar-package*) =>  FOO:FOO, :EXTERNAL

(find-symbol "FOO" *baz-package*) =>  FOO:FOO, :INHERITED
(find-symbol "BAR" *baz-package*) =>  BAR:BAR, :INHERITED

(packagep *foo-package*) =>  true
(packagep *bar-package*) =>  true
(packagep *baz-package*) =>  true

(package-name *foo-package*) =>  "FOO"
(package-name *bar-package*) =>  "BAR"
(package-name *baz-package*) =>  "BAZ"

(package-use-list *foo-package*) =>  ()
(package-use-list *bar-package*) =>  (#<PACKAGE "FOO">)
(package-use-list *baz-package*) =>  (#<PACKAGE "BAR">)

(package-used-by-list *foo-package*) =>  (#<PACKAGE "BAR">)
(package-used-by-list *bar-package*) =>  (#<PACKAGE "BAZ">)
(package-used-by-list *baz-package*) =>  ()

(delete-package *bar-package*)
>>  Error: Package BAZ uses package BAR.
>>  If continued, BAZ will be made to unuse-package BAR,
>>  and then BAR will be deleted.
>>  Type :CONTINUE to continue.
>>  Debug> :CONTINUE
=>  T

(symbol-package *foo-symbol*) =>  #<PACKAGE "FOO">
(symbol-package *bar-symbol*) is unspecified

(prin1-to-string *foo-symbol*) =>  "FOO:FOO"
(prin1-to-string *bar-symbol*) is unspecified

(find-symbol "FOO" *bar-package*) is unspecified

(find-symbol "FOO" *baz-package*) =>  NIL, NIL
(find-symbol "BAR" *baz-package*) =>  NIL, NIL

(packagep *foo-package*) =>  T
(packagep *bar-package*) =>  T
(packagep *baz-package*) =>  T

(package-name *foo-package*) =>  "FOO"
(package-name *bar-package*) =>  NIL
(package-name *baz-package*) =>  "BAZ"

(package-use-list *foo-package*) =>  ()
(package-use-list *bar-package*) is unspecified
(package-use-list *baz-package*) =>  ()

(package-used-by-list *foo-package*) =>  ()
(package-used-by-list *bar-package*) is unspecified
(package-used-by-list *baz-package*) =>  ()
```


## 影響

なし。


## 例外

もし*package*指定子が現在有効な名前ではなかったとき、
修正可能な型[`program-error`](5.3.program-error.html)のエラーが通知されます。
もし修正がされたとき、削除の処理は実施されず、
かわりに[`delete-package`](11.2.delete-package.html)は即座に[`nil`](5.3.nil-variable.html)を返却します。

もし*package*が他のパッケージに使われていたときは、
修正可能な型[`program-error`](5.3.program-error.html)のエラーが通知されます。
もし修正がされたとき、
`unuse-package`は効果的に呼ばれて
依存しているものを取り除き、
*package*の外部シンボルは
*package*を使っているそれらのパッケージで
アクセスできなくなります。
したがって[`delete-package`](11.2.delete-package.html)は
ちょうどそれを使うパッケージが存在しなくなるかのように
*package*を削除します。


## 参考

`unuse-package`


## 備考

なし。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

