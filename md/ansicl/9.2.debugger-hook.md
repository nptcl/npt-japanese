% Variable \*DEBUGGER-HOOK\*

[UP](9.2.html)  

---

# Variable **\*DEBUGGER-HOOK\***


Variable `*DEBUGGER-HOOK*`


## 値の型

2つの引数（コンディションと、
デバッガーに入った時点での[`*debugger-hook*`](9.2.debugger-hook.html)の値）
の関数指定子か、[`nil`](5.3.nil-variable.html)。


## 初期値

[`nil`](5.3.nil-variable.html)


## 定義

[`*debugger-hook*`](9.2.debugger-hook.html)の値が[`nil`](5.3.nil-variable.html)ではない場合、
[`invoke-debugger`](9.2.invoke-debugger.html)の呼び出しによるものか、
あるいは捕捉されないコンディションを
[`error`](9.2.error-function.html)か[`cerror`](9.2.cerror.html)で呼び出して
自動的にデバッガーに入ろうとしたとき、
この変数の値のものが通常の方法でデバッガーに入る前に呼び出されます。
この関数は、コンディションを捕捉するか（遷移制御）、
あるいは普通に終了するか（標準デバッガーが実行される）の
どちらかになります。
デバッグ中に再帰エラーを抑えるため、
その関数が呼ばれる前に[`invoke-debugger`](9.2.invoke-debugger.html)によって
[`*debugger-hook*`](9.2.debugger-hook.html)が[`nil`](5.3.nil-variable.html)に束縛されます。


## 例文

```lisp
(defun one-of (choices &optional (prompt "Choice"))
  (let ((n (length choices)) (i))
    (do ((c choices (cdr c)) (i 1 (+ i 1)))
        ((null c))
      (format t "~&[~D] ~A~%" i (car c)))
    (do () ((typep i `(integer 1 ,n)))
      (format t "~&~A: " prompt)
      (setq i (read))
      (fresh-line))
    (nth (- i 1) choices)))

(defun my-debugger (condition me-or-my-encapsulation)
  (format t "~&Fooey: ~A" condition)
  (let ((restart (one-of (compute-restarts))))
    (if (not restart) (error "My debugger got an error."))
    (let ((*debugger-hook* me-or-my-encapsulation))
      (invoke-restart-interactively restart))))

(let ((*debugger-hook* #'my-debugger))
  (+ 3 'a))
>>  Fooey: The argument to +, A, is not a number.
>>   [1] Supply a replacement for A.
>>   [2] Return to Cloe Toplevel.
>>  Choice: 1
>>   Form to evaluate and use: (+ 5 'b)
>>   Fooey: The argument to +, B, is not a number.
>>   [1] Supply a replacement for B.
>>   [2] Supply a replacement for A.
>>   [3] Return to Cloe Toplevel.
>>  Choice: 1
>>   Form to evaluate and use: 1
=>  9
```


## 影響

[`invoke-debugger`](9.2.invoke-debugger.html)


## 参考

なし。


## 備考

ユーザーが対話形式で打ち込んだコードを評価するとき、
フック関数がその第2引数である関数に
[`*debugger-hook*`](9.2.debugger-hook.html)を束縛すると、
同じ対話的機能を使用して再帰的エラーを処理することができるので
便利なことがあります。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

