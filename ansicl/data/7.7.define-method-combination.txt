;;
;;  ANSI Common Lisp 日本語訳
;;  7. オブジェクト
;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  Macro DEFINE-METHOD-COMBINATION
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@begin: 7.7.define-method-combination
@name: define-method-combination
@type: macro

Macro `DEFINE-METHOD-COMBINATION`


## {syntax}

`define-method-combination` *name* [[short-form-option]]  
=> *name*

`define-method-combination` *name* *lambda-list*
 (*method-group-specifier\**)
 [(:arguments . *args-lambda-list*)]
 [(:generic-function *generic-function-symbol*)]
 [[*declaration\** | *documentation*]]
 *form\**  
=> *name*

```
short-form-option::= :documentation documentation |  
                     :identity-with-one-argument identity-with-one-argument |
                     :operator operator
method-group-specifier::= (name {qualifier-pattern+ | predicate} [[long-form-option]])
long-form-option::= :description description |
                    :order order |
                    :required required-p
```


## {arguments-and-values}

*args-lambda-list* - define-method-combination引数のラムダリスト  
*declaration* - 宣言の式、評価はされません  
*description* - {format-control}  
*documentation* - 文字列、評価はされません
*form* - {implicit-progn}。
有効なメソッドをどのようにして結合させるかを計算し、
フォームとして返却しなければなりません。  
*generic-function-symbol* - シンボル  
*identity-with-one-argument* - {generalized-boolean}  
*lambda-list* - {ordinary-lambda-list}  
*name* - シンボル。
通常、キーワードではなく、{nil!variable:link}でもないシンボルが使われます。  
*operator* - オペレーター。
*name*と*operator*は、よく同じシンボルが使われます。
これがデフォルトですが、必須ではありません。  
*order* - `:most-specific-first`か`:most-specific-last`。評価されます。  
*predicate* - シンボルであり、
それはひとつの引数を取り{generalized-boolean}を返却する関数名。  
*qualifier-pattern* - リストか、シンボル`*`  
*required-p - {generalized-boolean}


## {description}

マクロ{define-method-combination:link}は、
新しいmethod-combinationの型を定義するときに使います。

{define-method-combination:link}には2つのフォームがあります。
短いフォームは単純な機能であり、
もっとも一般的に必要があると予想されるケースに対応しています。
長いフォームはより強力で、より冗長です。
これは、{defmacro:link}のように、
通常はボディ部がバッククォートを使った式であり、
フォームを計算します。
したがって、任意の制御構造を実装することができます。
長いフォームは、メソッド{qualifiers}を任意に処理することもできます。


- 短いフォーム
  - {define-method-combination:link}の短いフォームの構文は、
    二番目のサブフォームが{nil!variable:link}ではないシンボルか、
    あるいは現れていないときに認識されます。
    短いフォームを使うと、
    *name*が`(operator method-call method-call ...)`という
    Lispのフォームが生成されるmethod-combinationの型として定義されます。
    *operator*は関数か、マクロか、
    {special-operator}の名前のシンボルです。
    *operator*はキーワードオプションによって指定することができ、
    デフォルト値は*name*です。
    <br><br>
    短いフォームのキーワードオプションは下記の通り。
    - `:documentation`オプションは、
      method-combination型のドキュメントに使われます。
      定義は下記にある長いフォームを参照。
    - `:identity-with-one-argument`オプションは、
      値が*true*のときに最適化を有効にします。
      （デフォルトは*false*です。）
      もし適用可能なメソッドが正確にひとつであり、
      それがプライマリメソッドのとき、
      そのメソッドは有効なメソッドとして機能し、
      *operator*を呼び出しません。
      この最適化は、新たに有効なメソッドを作成することがなくなり、
      また関数呼び出しのオーバーヘッドを抑えます。
      このオプションは
      {progn:link}, {and:link}, {+:link}, {max:link}のようなオペレーターで
      で使用することを想定しています。
    - `:operator`オプションは、オペレーターの名前を指定します。
      *operator*引数は、関数か、マクロか、
      {special-operator}の名前のシンボルです。
  - これらのmethod-combinationの型は、
    メソッドに対して正確にひとつの{qualifiers}を要求します。
    もし適用可能なメソッドに、
    {qualifiers}がなかったり、
    method-combinationの型がサポートされていない
    {qualifiers}であったときは、
    エラーが発生します。
    <br><br>
    このように定義されたmethod-combinationの機能は、
    メソッドに2つの役割を与えます。
    このタイプのmethod-combinationによって名前付けられたシンボルが
    ひとつの{qualifiers}として与えられたメソッドは、
    プライマリメソッドとして定義されます。
    少なくともひとつのプライマリメソッドが
    適用可能でないとエラーが発生します。
    ひとつの{qualifiers}として`:around`が与えられたメソッドは、
    `standard` method-combinationのaroundメソッドと
    同じふるまいをする補助メソッドです。
    関数{call-next-method:link}は、aroundメソッド内でのみ使用できます。
    つまり、{define-method-combination:link}マクロの
    短いフォームによって定義された
    プライマリメソッド内では使用することができません。
    <br><br>
    この方法で定義されたmethod-combinationの機能は、
    オプション引数*order*を受け取ります。
    デフォルト値は`:most-specific-first`です。
    値`:most-specific-last`は、
    補助メソッドの順番に影響することなく、
    プライマリメソッドの順番を逆順にさせます。
    <br><br>
    短いフォームは、自動的にエラーチェックを含み、
    またaroundメソッドもサポートします。
    <br><br>
    組み込みのmethod-combination型の説明は、{7.6.6.4:ab}を参照してください。
    <br><br>

- 長いフォーム
  - {define-method-combination:link}の長いフォームの構文は、
    二番目のサブフォームがリストのときに認識されます。
    <br><br>
    *lambda-list*は、{defgeneric:link}の`:method-combination`オプションの
    型method-combinationの名前の後に提供されている
    どのような引数も受け取ります。
    <br><br>
    メソッドグループのリストについては次の通り。
    各指定子は、パターンとマッチする{qualifiers}か、
    *predicate*でテストされた{qualifiers}か、どちらかの判定によって、
    適用可能なメソッド特定の役割を果たすためのサブセットを選択します。
    これらのメソッドグループ指定子は、
    このmethod-combination型で使用できる
    全てのメソッド{qualifiers}を定義します。
    <br><br>
    各*method-group-specifier*の`car`部は、変数名のシンボルです。
    {define-method-combination:link}のボディ部のフォームを実行しているとき、
    この変数はメソッドグループ内のメソッドのリストに束縛されます。
    このリスト中のメソッドは、`:order`オプションによって順序付けられます。
    <br><br>
    もし*qualifier-pattern*がシンボルなら、それは`*`出なければなりません。
    もしメソッドの{qualifiers}のリストと
    *qualifier-pattern*が{equal:link}で一致する場合
    （ただしシンボル`*`はどんな*qualifier-pattern*にもマッチします）、
    メソッドは*qualifier-pattern*にマッチすると言えます。
    したがって、*qualifier-pattern*は、次に示すうちのひとつに当てはまります。
    空のリストの場合は、{qualifiers}がないメソッドにマッチします。
    シンボル`*`は、すべてのメソッドにマッチします。
    通常のリストのときは、
    メソッドにはリストの長さと同じ数の{qualifiers}があり、
    各{qualifiers}が対応するリストの要素にマッチします。
    もしドットリストのときは、シンボル`*`で終端します
    （`*`は、続くどんな数の{qualifiers}にもマッチします）。
    <br><br>
    適用可能な各メソッドは、左から右の順番で、
    *qualifier-patterns*と*predicate*のテストが行われます。
    *qualifier-patterns*か*predicate*が*true*のときは、
    そのメソッドはすぐに対応するメソッドグループのメンバーとなり、
    それ以上のテストは行われません。
    したがって、メソッドグループが複数の*qualifier-pattern*を持つ場合、
    メソッドはそのうちの1つの*qualifier-pattern*を満たすだけで
    グループのメンバとなることができます。
    <br><br>
    もしどのメソッドグループにも属さない適用可能なメソッドがある場合は、
    関数{invalid-method-error:link}が呼び出されます。
    <br><br>
    メソッドグループは、*qualifier-patter*と*predicate*に続いて
    キーワードオプションを指定することができます。
    キーワードオプションはリストでもシンボル`*`でもないので、
    追加の*qualifier-pattern*と区別することができます。
    キーワードオプションは次の通りです。
    - `:description`オプションは、
      メソッドグループ内のメソッドの役割の説明を指定するときに使われます。
      プログラミング環境のツールは、
      簡潔な説明を出力するときは、
      `(apply #'format stream format-control (method-qualifiers method))`を
      実行することを期待しています。
      ほとんどの場合、*format-control*は{format-directive}を含みませんが、
      一般性のために利用できます。
    - `:order`オプションは、メソッドの順序を指定します。
      *order*引数は、`:most-specific-first`か`:most-specific-last`に
      評価されるフォームです。
      もしそのほかの値に評価された場合はエラーが発生します。
      `:order`が指定されなかったときは、デフォルトは`:most-specific-first`です。
    - `:require`オプションは、
      メソッドグループに少なくともひとつのメソッドが必要かどうかを指定します。
      もし値が*true*であり、かつメソッドグループが空のとき
      （これは、適用可能なメソッドが、*qualifier-pattern*にマッチしないか
      あるいは*predicate*を満たさなかったとき）
      エラーが発生します。
      `:require`が指定されないときは、デフォルトは{nil!variable:link}です。
  - メソッドグループ指定子を使用することで、
    メソッドを選択し、可能な役割にあうように分け、必要なエラーチェックを行うのに、
    便利な構文が提供されます。
    ボディ部のフォームにて、通常のリスト処理を行い、
    {method-qualifiers:link}と{invalid-method-error:link}の関数を使用することで、
    さらなるメソッドのフィルタリングを行うことができます。
    メソッドグループ指定子内で名前付けられた変数に
    {setq:link}を使うこともできますし、
    追加の変数に束縛することもできます。
    また、メソッドグループ指定子の仕組みをバイパスして、
    ボディ部のフォーム内ですべてを行うこともできます。
    これは、*qualifier-patter*が`*`のみである
    単体のメソッドグループを記述することで実現できます。
    変数にはすべての適用可能なメソッドのリストが
    `most-specific-first`順に束縛されます。
    <br><br>
    ボディ部*form*では、有効なメソッドをどのようにして結合させるかを計算し、
    フォームとして返却します。
    有効なメソッドは`null`の{lexical-environment}から
    加えてローカルマクロ{call-method:link}の定義と、
    `COMON-LISP-USER`パッケージからアクセスできないシンボル名の束縛で
    拡張された環境下で評価されます。
    メソッドグループ指定子によって生成されたリストのうちの
    ひとつのメソッドオブジェクトと、次のメソッドのリストが与えられると、
    {call-method:link}は、{call-next-method:link}が次のメソッドとして利用可能な
    メソッドを呼び出します。
    <br><br>
    ある有効なメソッドが、
    単体のメソッドを呼び出す他に効果を持たないとき、
    単体のメソッドをそのまま有効なメソッドとして呼び出し、
    新たに有効なメソッドの生成をする必要がないように
    最適化する実装もあります。
    この最適化は、有効なメソッドのフォームが、
    最初のフォームはメソッドオブジェクトで、
    二番目のフォームは{nil!variable:link}か
    あるいは指定されていないような{call-method:link}マクロの呼び出しで
    全体が構成されているような場合において有効です。
    各{define-method-combination:link}のボディ部は
    この最適が必要な場合は、
    {progn:link}, {and:link}, {multiple-value-prog1:link}などの
    冗長な呼び出しを削除する責任を負います。
    <br><br>
    リスト`(:arguments . lambda-list)`は、
    宣言かドキュメント文字の前に記述することができます。
    このフォームは、method-combinationが
    メソッドを結合するための一部としてある特定の動作を行い、
    続けてジェネリック関数の引数にアクセスする
    必要が生じた場合において有効です。
    *lambda-list*によって定義された各パラメーターの変数は、
    有効なメソッドの中に挿入されたフォーム内で束縛されます。
    この*form*が有効なメソッドの実行中に評価されるとき、
    それらの値はジェネリック関数に対応する引数になります。
    そのようなフォームを{setf:link}の`place`として使用した時の結果は未定です。
    引数の対応は、`:arguments`の*lambda-list*と、
    ジェネリック関数の*lambda-list*を、
    それぞれ次の三つのセクションに分けて計算します。
    その三つは、{required-parameter}、オプションパラメーター、
    そして、キーワードと`rest`パラメーターです。
    ある特定の呼び出し時にジェネリック関数に指定された引数もまた、
    三つのセクションに分割されます。
    {required-arguments}のセクションは、
    ジェネリック関数が保有する{required-arguments}と同じ数の引数を含み、
    オプション引数のセクションは
    ジェネリック関数が保有するオプション引数と同じ数の引数を含み、
    そしてキーワードと`rest`引数のセクションは残りの引数を含みます。
    `:arguments`の*lambda-list*にある
    {required-arguments}とオプション引数の
    セクション内にある各パラメーターは、
    対応する引数のセクションと
    同じ位置にある引数にアクセスします。
    もし`arguments`の*lambda-list*のセクションの方が短いときは、
    余分な引数は無視されます。
    もし`arguments`の*lambda-list*のセクションの方が長いときは、
    超過した{required-parameter}は{nil!variable:link}が束縛され、
    超過したオプションパラメーターはそれぞれの`initform`に束縛されます。
    `:arguments`の*lambda-list*のキーワードと`rest`パラメーターは、
    引数のキーワードと`rest`のセクションにアクセスされます。
    もし`:arguments`の*lambda-list*に`&key`が含まれているときは、
    `&allow-other-keys`が含まれているかのようにふるまいます。
    <br><br>
    さらに、`:arguments`の*lambda-list*の最初に
    `&whole var`配置することができます。
    これは、ジェネリック関数に指定されたすべての引数のリストを、
    評価される*form*に対して*var*に束縛します。
    これはキーワードや`rest`引数だけでなく、
    すべての引数にアクセスするので、`&rest`とは異なります。
    <br><br>
    ボディ部で検出されたエラーコンディションは、
    {method-combination-error:link}か{invalid-method-error:link}で
    報告するべきです。
    これらの関数は、エラーメッセージに必要な文脈的情報を追加し、
    そして適切なエラーを通知します。
    <br><br>
    ボディ部*form*は、メソッドグループ指定子と
    ラムダリストによって作成された束縛の内側で評価されます。
    ボディ部の先頭にある宣言は、
    ラムダリストにの束縛の内側に直接配置され、
    そしてメソッドグループの変数の束縛の外側に配置されます。
    したがって、この方法では、メソッドグループの変数は宣言できません。
    ただし、ボディ部の周囲に局所的に使用することはできます。
    <br><br>
    ボディ部*form*の中では、*generic-function-symbol*は
    ジェネリック関数オブジェクトが束縛されます。
    <br><br>
    *documentation*は、ドキュメント文字が*name*として、
    （種類は{method-combination!symbol:link}で）
    method-combinationオブジェクトに割り当てられます。
    <br><br>
    同じ特定子の二つのメソッドが、それぞれ違う{qualifiers}を持つとき、
    メソッド選択と結合の過程を定義した{7.6.6:ab}の
    ステップ2で定義されているアルゴリズムでは、
    順序付けされないことに注意してください。
    通常、2つのメソッドは{qualifiers}が異なるので、
    有効なメソッド内では違う役割をします。
    しかしステップ2でどのように並び変えても、
    有効なメソッドは同じです。
    もし二つのメソッドが同じ役割であり
    それらの順序が重要な時はエラーが発生します。
    これは{define-method-combination:link}の{qualifiers}の
    パターンマッチの部分で発生します。

もし{define-method-combination:link}フォームがトップレベルフォームに現れたとき、
コンパイラーはmethod-combinationの名前が、
続く{defgeneric:link}フォームのmethod-combination名で
有効であると認識させなければなりません。
しかし、method-combinationが実行されるのは、
{define-method-combination:link}フォームの実行時より早くはないので、
ジェネリック関数がmethod-combinationを使うときまで
可能な限り遅らせて実行されます。


## {examples}

{define-method-combination:link}の長いフォームの例のほとんどは、
メソッド結合の機能の宣言の一部として、
提供されている関係関数の使用も説明をしています。

```lisp
;;; define-method-combinationの短いフォームの例
 
(define-method-combination and :identity-with-one-argument t)
 
(defmethod func and ((x class1) y) ...)
 
;;; このサンプルと同等の長いフォームの例:
 
(define-method-combination and
        (&optional (order :most-specific-first))
        ((around (:around))
         (primary (and) :order order :required t))
  (let ((form (if (rest primary)
                  `(and ,@(mapcar #'(lambda (method)
                                      `(call-method ,method))
                                  primary))
                  `(call-method ,(first primary)))))
    (if around
        `(call-method ,(first around)
                      (,@(rest around)
                       (make-method ,form)))
        form)))
 
;;; define-method-combinationの長いフォームの例
 
;The default method-combination technique
(define-method-combination standard ()
        ((around (:around))
         (before (:before))
         (primary () :required t)
         (after (:after)))
  (flet ((call-methods (methods)
           (mapcar #'(lambda (method)
                       `(call-method ,method))
                   methods)))
    (let ((form (if (or before after (rest primary))
                    `(multiple-value-prog1
                       (progn ,@(call-methods before)
                              (call-method ,(first primary)
                                           ,(rest primary)))
                       ,@(call-methods (reverse after)))
                    `(call-method ,(first primary)))))
      (if around
          `(call-method ,(first around)
                        (,@(rest around)
                         (make-method ,form)))
          form))))
  
;どれかがnon-nilを返却するまでメソッドを実行する単純な例
(define-method-combination or ()
        ((methods (or)))
  `(or ,@(mapcar #'(lambda (method)
                     `(call-method ,method))
                 methods)))
  
;先行のより完成されたバージョン
(define-method-combination or
        (&optional (order ':most-specific-first))
        ((around (:around))
         (primary (or)))
  ;; Process the order argument
  (case order
    (:most-specific-first)
    (:most-specific-last (setq primary (reverse primary)))
    (otherwise (method-combination-error "~S is an invalid order.~@
    :most-specific-first and :most-specific-last are the possible values."
                                         order)))
  ;; Must have a primary method
  (unless primary
    (method-combination-error "A primary method is required."))
  ;; Construct the form that calls the primary methods
  (let ((form (if (rest primary)
                  `(or ,@(mapcar #'(lambda (method)
                                     `(call-method ,method))
                                 primary))
                  `(call-method ,(first primary)))))
    ;; Wrap the around methods around that form
    (if around
        `(call-method ,(first around)
                      (,@(rest around)
                       (make-method ,form)))
        form)))
  
;オプション:orderと:requireを使った同じ例
(define-method-combination or
        (&optional (order ':most-specific-first))
        ((around (:around))
         (primary (or) :order order :required t))
  (let ((form (if (rest primary)
                  `(or ,@(mapcar #'(lambda (method)
                                     `(call-method ,method))
                                 primary))
                  `(call-method ,(first primary)))))
    (if around
        `(call-method ,(first around)
                      (,@(rest around)
                       (make-method ,form)))
        form)))
  
;この短いフォームの呼出しは先行と同一のふるまいをする
(define-method-combination or :identity-with-one-argument t)
 
;qualifiersの正の整数によるメソッドの順序付け
;この小さな例では:aroundメソッドは許されない
(define-method-combination example-method-combination ()
        ((methods positive-integer-qualifier-p))
  `(progn ,@(mapcar #'(lambda (method)
                        `(call-method ,method))
                    (stable-sort methods #'<
                      :key #'(lambda (method)
                               (first (method-qualifiers method)))))))

(defun positive-integer-qualifier-p (method-qualifiers)
  (and (= (length method-qualifiers) 1)
       (typep (first method-qualifiers) '(integer 0 *))))
  
;;; 引数:argumentsの使用例
(define-method-combination progn-with-lock ()
        ((methods ()))
  (:arguments object)
  `(unwind-protect
       (progn (lock (object-lock ,object))
              ,@(mapcar #'(lambda (method)
                            `(call-method ,method))
                        methods))
     (unlock (object-lock ,object))))
```


## {affected-by}

なし。


## {side-effects}

コンパイラーはコンパイル時の副次的な処理を行う必要はありません。


## {exceptional-situations}

短いフォームで定義されたmethod-combinationの型は、
メソッドに対して正確にひとつの{qualifiers}を要求します。
もし適用可能なメソッドに、{qualifiers}がなかったり、
method-combinationの型がサポートされていない
{qualifiers}であったときは、
型{error!condition:link}のエラーが発生します。
少なくともひとつのプライマリメソッドが適用可能でないと
型{error!condition:link}のエラーが発生します。

もしどのメソッドグループにも属さない適用可能なメソッドがある場合は、
システムは、型{error!condition:link}のエラーを通知し、
そのメソッドが使用中のmethod-combinationの種類に対して無効であることを示します。

もし`:require`オプションの値が*true*であり、かつメソッドグループが空のとき
（これは、適用可能なメソッドが、*qualifier-pattern*にマッチしないか
あるいは*predicate*を満たさなかったとき）
型{error!condition:link}のエラーが発生します。

もし*order*引数が評価された値が、
`:most-specific-first`か`:most-specific-last`の
どちらでもなかった場合は型{error!condition:link}のエラーが発生します。


## {see-also}

{call-method:link},
{call-next-method:link},
{documentation:link},
{method-qualifiers:link},
{method-combination-error:link},
{invalid-method-error:link},
{defgeneric:link},
{7.6.6:ab},
{7.6.6.4:ab},
{3.4.11:ab}


## {notes}

{defgeneric:link}の`:method-combination`オプションは、
ジェネリック関数が特定のmethod-combination型を使用するように
指定するときに使われます。
`:method-combination`オプションの最初の引数はmethod-combination型の名前であり、
残りの引数はその型へのオプションです。

@end

