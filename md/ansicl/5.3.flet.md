% Special-Operator FLET, LABELS, MACROLET

[UP](5.3.html)  

---

# Special-Operator **FLET, LABELS, MACROLET**


Special Operator `FLET`, `LABELS`, `MACROLET`


## 構文

`flet` ((*function-name* *lambda-list*
 [[*local-declaration\** `|` *local-documentation*]] *local-form\**)\*)
 *declaration\** *form\**  
=> *result\**

`labels` ((*function-name* *lambda-list*
 [[*local-declaration\** `|` *local-documentation*]] *local-form\**)\*)
 *declaration* form*
 *declaration\** *form\**  
=> *result\**

`macrolet` ((*name* *lambda-list*
 [[*local-declaration\** `|` *local-documentation*]] *local-form\**)\*)
 *declaration\** *form\**  
=> *result\**


## 引数と戻り値

*function-name* - 関数名  
*name* - シンボル  
*lambda-list* - ラムダリスト。
[`flet`](5.3.flet.html)と[`labels`](5.3.flet.html)は通常のラムダリスト。
[`macrolet`](5.3.flet.html)はマクロラムダリスト。  
*local-declaration* - 宣言式。評価されません。  
*declaration* - 宣言式。評価されません。  
*local-documentation* - 文字列。評価されません。  
*local-form*, *form* - 暗黙のprogn  
*result* - *form*の返却値


## 定義

[`flet`](5.3.flet.html)、[`labels`](5.3.flet.html)、[`macrolet`](5.3.flet.html)は、
ローカル関数とローカルマクロを定義し、
そのローカルな定義を使い*form*を実行します。
*form*は、現れた順に実行します。

[`flet`](5.3.flet.html)と[`labels`](5.3.flet.html)によって作成された各関数と、
[`macrolet`](5.3.flet.html)によって作成された各マクロの
それぞれのボディ部の*form*（ただしラムダリストは除く）は、
必要に応じて、
*function-name*の関数の`block`名か*name*を用いて、
暗黙の`block`に囲まれます。

ローカル関数とマクロの定義部のリストと、
[`flet`](5.3.flet.html)および[`labels`](5.3.flet.html)のボディ部*form*間における
*declaration*のスコープは、
ローカルの関数定義の本体が含まれません。
ただし例外は[`labels`](5.3.flet.html)の、
[`inline`](3.8.inline.html)、[`notinline`](3.8.inline.html)、[`ftype`](3.8.ftype.html)宣言であり、
これらはローカルの関数定義を参照し、
そのローカル関数の本体に適用します。
つまり、これらの宣言のスコープは、
関数名として影響を与える範囲と同じです。
これらの*declaration*のスコープには、
[`macrolet`](5.3.flet.html)で定義されたマクロ展開数の本体は含まれません。

- [`flet`](5.3.flet.html)
  - [`flet`](5.3.flet.html)は、ローカルに名前がある関数を定義し、
    それらの定義を束縛とともに一連の*form*を実行します。
    そのようなローカル関数はいくつでも定義できます。
    <br><br>
    名前が束縛されたスコープは、ただボディ部のみ含まれます。
    [`flet`](5.3.flet.html)のボディ部内では、
    [`flet`](5.3.flet.html)によって定義された*function-name*という名前は、
    同名のグローバルに宣言されたものではなく、
    ローカルに宣言された関数が参照されます。
    [`flet`](5.3.flet.html)のスコープ内では、
    [`flet`](5.3.flet.html)によって定義されている
    *function-name*という名前の、
    グローバルのsetf-expanderの定義は適用されません。
    これは、`(defmethod (setf f) ...)`ではなく、
    `(defsetf f ...)`に適用されることに注意してください。
    <br><br>
    [`flet`](5.3.flet.html)によって定義された関数名は、レキシカルな環境内のものであり、
    それらのローカルな定義は、ただ[`flet`](5.3.flet.html)本体内でのみ保持されます。
    関数定義の束縛は、[`flet`](5.3.flet.html)本体でのみ見ることができ、
    それらの定義自身では見ることができません。
    関数定義内において、
    ローカルの関数名で定義されているものを探すときは、
    [`flet`](5.3.flet.html)の外側で定義された関数かマクロが参照されます。
    [`flet`](5.3.flet.html)は、局所的にグローバル関数名を隠蔽することができ、
    その新しい定義は、グローバルな定義を参照することができます。
    <br><br>
    どんな*local-documentation*も、
    対応するローカル関数（もし実際に作成されたとき）に、
    ドキュメント文字として割り当てます。
    <br><br>

- [`labels`](5.3.flet.html)
  - [`labels`](5.3.flet.html)は、[`flet`](5.3.flet.html)と同じですが、
    [`labels`](5.3.flet.html)の場合は定義された関数名のスコープが、
    ボディ部と同じように関数定義自身にも囲まれるところが違っています。
    <br><br>

- [`macrolet`](5.3.flet.html)
  - [`macrolet`](5.3.flet.html)は、[`defmacro`](3.8.defmacro.html)と同じフォーマットで、
    ローカルマクロの定義を確立します。
    <br><br>
    [`macrolet`](5.3.flet.html)の内部では、
    [`macrolet`](5.3.flet.html)によって定義されている
    *function-name*という名前の、
    グローバルのsetf-expanderの定義は適用されません。
    そうではなく、[`setf`](5.3.setf.html)はマクロフォームを展開し、
    結果のフォームを再帰的に処理します。
    <br><br>
    [`macrolet`](5.3.flet.html)によって定義されたマクロ展開関数は、
    [`macrolet`](5.3.flet.html)のフォームに現れる、
    レキシカルな環境によって定義されたものです。
    宣言と、[`macrolet`](5.3.flet.html)と[`symbol-macrolet`](3.8.symbol-macrolet.html)の定義は、
    [`macrolet`](5.3.flet.html)内の、ローカルマクロ定義に影響を及ぼします。
    しかしもしローカルマクロ定義から
    レキシカルな環境内で見ることができる
    ローカルな変数か関数を参照したときの結果は未定義です。
    <br><br>
    どんな*local-documentation*も、
    対応するローカルマクロ関数に、
    ドキュメント文字として割り当てます。
    <br><br>


## 例文

```lisp
(defun foo (x flag)
  (macrolet ((fudge (z)
                ; パラメーターxとflagはこの時点ではアクセス不可です。
                ; flagの参照はグローバル変数の名前になります。
                ` (if flag (* ,z ,z) ,z)))
   ; パラメーターxとflagは、ここではアクセス可能です。
    (+ x
       (fudge x)
       (fudge (+ x 1)))))
== 
(defun foo (x flag)
  (+ x
     (if flag (* x x) x)
     (if flag (* (+ x 1) (+ x 1)) (+ x 1))))
```

これは、マクロ展開が行われた後です。
`x`と`flag`の発生は、
関数`foo`のパラメータを合法的に参照しています。
なぜなら、これらのパラメータは、
マクロが呼ばれて展開が生成された場所では
見ることができるからです。


```lisp
(flet ((flet1 (n) (+ n n)))
   (flet ((flet1 (n) (+ 2 (flet1 n))))
     (flet1 2))) =>  6

(defun dummy-function () 'top-level) =>  DUMMY-FUNCTION 
(funcall #'dummy-function) =>  TOP-LEVEL 
(flet ((dummy-function () 'shadow)) 
     (funcall #'dummy-function)) =>  SHADOW 
(eq (funcall #'dummy-function) (funcall 'dummy-function))
=>  true 
(flet ((dummy-function () 'shadow))
  (eq (funcall #'dummy-function)
      (funcall 'dummy-function)))
=>  false 

(defun recursive-times (k n)
  (labels ((temp (n) 
             (if (zerop n) 0 (+ k (temp (1- n))))))
    (temp n))) =>  RECURSIVE-TIMES
(recursive-times 2 3) =>  6

(defmacro mlets (x &environment env) 
   (let ((form `(babbit ,x)))
     (macroexpand form env))) =>  MLETS
(macrolet ((babbit (z) `(+ ,z ,z))) (mlets 5)) =>  10
(flet ((safesqrt (x) (sqrt (abs x))))
 ;; `safesqrt`関数は、二か所で使用されます。
  (safesqrt (apply #'+ (map 'list #'safesqrt '(1 2 3 4 5 6)))))
=>  3.291173
(defun integer-power (n k)     
  (declare (integer n))         
  (declare (type (integer 0 *) k))
  (labels ((expt0 (x k a)
             (declare (integer x a) (type (integer 0 *) k))
             (cond ((zerop k) a)
                   ((evenp k) (expt1 (* x x) (floor k 2) a))
                   (t (expt0 (* x x) (floor k 2) (* x a)))))
           (expt1 (x k a)
             (declare (integer x a) (type (integer 0 *) k))
             (cond ((evenp k) (expt1 (* x x) (floor k 2) a))
                   (t (expt0 (* x x) (floor k 2) (* x a))))))
   (expt0 n k 1))) =>  INTEGER-POWER
(defun example (y l)
  (flet ((attach (x)
           (setq l (append l (list x)))))
    (declare (inline attach))
    (dolist (x y)
      (unless (null (cdr x))
        (attach x)))
    l))

(example '((a apple apricot) (b banana) (c cherry) (d) (e))
         '((1) (2) (3) (4 2) (5) (6 3 2)))
=>  ((1) (2) (3) (4 2) (5) (6 3 2) (A APPLE APRICOT) (B BANANA) (C CHERRY))
```


## 影響

なし。


## 例外

なし。

## 参考

[`declare`](3.8.declare.html),
[`defmacro`](3.8.defmacro.html),
[`defun`](5.3.defun.html),
[`documentation`](25.2.documentation.html),
[`let`](5.3.let.html),
3.1. 評価,
3.4.11. ドキュメント文字と宣言の文脈的な作用


## 備考

[`flet`](5.3.flet.html)では、再帰関数を定義することはできません。
[`labels`](5.3.flet.html)は、互いに再帰呼出できる関数を定義するのに使うことができます。

もし[`macrolet`](5.3.flet.html)がトップレベルフォームのとき、
ボディ部*form*もまたトップレベルフォームとして処理されます。
3.2.3. ファイルのコンパイルをご確認ください。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

