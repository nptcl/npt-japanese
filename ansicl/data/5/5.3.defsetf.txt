;;
;;  ANSI Common Lisp 日本語訳
;;  5. データと制御フロー
;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  Macro DEFSETF
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@begin: 5.3.defsetf
@name: defsetf
@type: macro

Macro `DEFSETF`


## {syntax}

短いフォーム:  
`defsetf` *access-fn* *update-fn* [*documentation*]  
=> *access-fn*

長いフォーム:  
`defsetf` *access-fn* *lambda-list* (*store-variable\**)
 [[*declaration\** `|` *documentation*]] *form\**  
=> *access-fn*


## {arguments-and-values}

*access-fn* - 関数かマクロの名前のシンボル  
*update-fn* - 関数かマクロの名前のシンボル  
*lambda-list* - {defsetf-lambda-list}  
*store-variable* - シンボル（変数名）  
*declaration* - 宣言式。評価されません。  
*documentation* - 文字列。評価されません。  
*form* - フォーム


## {description}

{defsetf:link}は、比較的簡単な場合の{place}のフォーム`(access-fn ...)`を
どのようにして{setf:link}で適用するか定義します
（より一般的なアクセスの機能については、
{define-setf-expander:link}をご確認下さい）。
*access-fn*という関数かマクロは、
それらの引数の全てを評価しなければなりません。

{defsetf:link}は、「短いフォーム」、「長いフォーム」と呼ばれる
2つのフォームのひとつを取ることができます。
それらは2番目の引数の型によって区別されます。

短いフォームを使うとき、*update-fn*は関数かマクロの名前でなければならず、
それは*access-fn*よりひとつ多い引数を取ります。
もし{setf:link}に与えられた{place}で*access-fn*が呼ばれたとき、
*access-fn*へ与えられたすべての引数と
最後の引数として新しい値を追加したものを
*update-fn*に指定して呼びし展開します
（最後の新しい値は*update-fn*によって返却する必要があります）。

{defsetf:link}の長いフォームは、{defmacro:link}に似ています。
*lambda-list*は*access-fn*の引数を定義します。
*store-variable*は{place}へ格納する値か多値を定義します。
*body*は*access-fn*の呼び出しの
{setf:link}への展開を計算する必要があります。
展開関数は{defsetf:link}フォームが現れたところと
同じ{lexical-environment}上で定義されます。

*form*を評価している間、*lambda-list*と*store-variable*の変数は、
{gensym:link}や{gentemp:link}によって生成された一時変数に束縛され、
{setf:link}の展開によってこれらのサブフォームの値に束縛されます。
この束縛によって、評価順の問題を考慮することなく
*form*を書くことができるようになります。
{defsetf:link}は、このような場合において可能であれば、
一時変数が最終的な値を最適化するように調整します。

{defsetf:link}のコード本体は、
暗黙的に*access-fn*という名前の`block`で囲まれます。

{defsetf:link}は{place}のサブフォームが
正確に一度だけ評価されることを保証します。

*documentation*は、ドキュメント文字の種別{setf!symbol:link}として
*access-fn*に割り当てをします。

もし{defsetf:link}フォームが{top-level-form}として現れたときは、
コンパイラは、そのファイルの後で{setf:link}が呼ばれたときに展開が行われるよう、
{setf-expander}を有効にしなければなりません。
もし同じファイルの後で{place}が*access-fn*を使用するときは、
ユーザーは、もしあるなら*form*をコンパイル時に
評価できることを保証しなければなりません。
コンパイラーは、{get-setf-expansion:link}の環境引数が
マクロの環境パラメータの値を受け取った場合、
コンパイル時にこれらの{setf-expander}を
利用できるようにしなければなりません。


## {examples}

次の式の効果は、Common Lispシステム内に構築されています。

```lisp
(defsetf symbol-value set)
```

これは`(setf (symbol-value foo) fu)`というフォームが
`(set foo fu)`に展開されます。

次の文に注意してください。

```lisp
(defsetf car rplaca)
```

この文は正しくはなく、
なぜなら{rplaca:link}は最後の引数を返却しないからです。

```lisp
(defun middleguy (x) (nth (truncate (1- (list-length x)) 2) x)) =>  MIDDLEGUY
(defun set-middleguy (x v)
   (unless (null x)
     (rplaca (nthcdr (truncate (1- (list-length x)) 2) x) v))
   v) =>  SET-MIDDLEGUY
(defsetf middleguy set-middleguy) =>  MIDDLEGUY
(setq a (list 'a 'b 'c 'd)
      b (list 'x)
      c (list 1 2 3 (list 4 5 6) 7 8 9)) =>  (1 2 3 (4 5 6) 7 8 9)
(setf (middleguy a) 3) =>  3
(setf (middleguy b) 7) =>  7
(setf (middleguy (middleguy c)) 'middleguy-symbol) =>  MIDDLEGUY-SYMBOL
a =>  (A 3 C D)
b =>  (7)
c =>  (1 2 3 (4 MIDDLEGUY-SYMBOL 6) 7 8 9)
```

{defsetf:link}の長いフォームの使用例。

```lisp
(defsetf subseq (sequence start &optional end) (new-sequence)
  `(progn (replace ,sequence ,new-sequence
                   :start1 ,start :end1 ,end)
          ,new-sequence)) =>  SUBSEQ
(defvar *xy* (make-array '(10 10)))
(defun xy (&key ((x x) 0) ((y y) 0)) (aref *xy* x y)) =>  XY
(defun set-xy (new-value &key ((x x) 0) ((y y) 0))
  (setf (aref *xy* x y) new-value)) =>  SET-XY
(defsetf xy (&key ((x x) 0) ((y y) 0)) (store)
  `(set-xy ,store 'x ,x 'y ,y)) =>  XY
(get-setf-expansion '(xy a b))
=>  (#:t0 #:t1),
  (a b),
  (#:store),
  ((lambda (&key ((x #:x)) ((y #:y))) 
     (set-xy #:store 'x #:x 'y #:y))
   #:t0 #:t1),
  (xy #:t0 #:t1)
(xy 'x 1) =>  NIL
(setf (xy 'x 1) 1) =>  1
(xy 'x 1) =>  1
(let ((a 'x) (b 'y))
  (setf (xy a 1 b 2) 3)
  (setf (xy b 5 a 9) 14))
=>  14
(xy 'y 0 'x 1) =>  1
(xy 'x 1 'y 2) =>  3
```


## {affected-by}

なし。


## {exceptional-situations}

なし。


## {see-also}

{documentation:link},
{setf:link},
{define-setf-expander:link},
{get-setf-expansion:link},
{5.1:ab},
{3.4.11:ab}


## {notes}

*form*には正しい値（*store-variable*の値か多値）の
返却のための規定を含めなければなりません。
これは{defsetf:link}によってではなく*form*によって処理します。
なぜなら、多くの場合この値は、その場所に保存して正しい値を返す関数を
同時に呼び出すことによって、余分なコストをかけずに返却できるからです。

{setf:link}の*access-fn*の呼び出しは、
*access-fn*の全ての引数もまた評価されます。
それはどの引数も特別に扱うことができません。
つまり{defsetf:link}は、`(ldb field reference)`のような
バイトの一般参照への格納方法を記述することができません。
{define-setf-expander:link}は、
{defsetf:link}によって課されている制限に
ふさわしくないような状況を処理するために使われ、
ユーザーに対して追加の制御を与えます。

@end

