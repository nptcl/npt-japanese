% Macro WITH-SLOTS

[UP](7.7.html)  

---

# Macro **WITH-SLOTS**


Macro `WITH-SLOTS`


## 構文

`with-slots` (*slot-entry\**) *instance-form* *declaration\** *form\**
=> *result\**  
*slot-entry* ::= *slot-name* `|` (*variable-name* *slot-name*)


## 引数と戻り値

slot-name - スロット名、評価はされない  
variable-name - 変数名、評価はされない  
instance-form - フォーム、評価されて*instance*が生成される  
instance - オブジェクト  
declaration - 宣言式、評価はされない  
forms - フォーム、暗黙のprogn  
results - フォームの返却値


## 定義

マクロ[`with-slots`](7.7.with-slots.html)は、*slot-name*で指定された*instance*のスロットを、
変数のように参照するためのレキシカルな環境が作成されます。
このような文脈では、スロットの値は、
レキシカルに束縛された変数のように、
そのスロット名を使用して使うことができます。
スロットの値を設定するために
`setf`と[`setq`](5.3.setq.html)の両方を使用することができます。

マクロ[`with-slots`](7.7.with-slots.html)は、スロット名が変数として現れるところを、
[`slot-value`](7.7.slot-value.html)の呼び出しに変換します。


## 例文

```lisp
(defclass thing ()
          ((x :initarg :x :accessor thing-x)
           (y :initarg :y :accessor thing-y)))
=>  #<STANDARD-CLASS THING 250020173>
(defmethod (setf thing-x) :before (new-x (thing thing))
  (format t "~&Changing X from ~D to ~D in ~S.~%"
          (thing-x thing) new-x thing))
(setq thing (make-instance 'thing :x 0 :y 1)) =>  #<THING 62310540>
(with-slots (x y) thing (incf x) (incf y)) =>  2
(values (thing-x thing) (thing-y thing)) =>  1, 2
(setq thing1 (make-instance 'thing :x 1 :y 2)) =>  #<THING 43135676>
(setq thing2 (make-instance 'thing :x 7 :y 8)) =>  #<THING 43147374>
(with-slots ((x1 x) (y1 y))
            thing1
  (with-slots ((x2 x) (y2 y))
              thing2
    (list (list x1 (thing-x thing1) y1 (thing-y thing1)
                x2 (thing-x thing2) y2 (thing-y thing2))
          (setq x1 (+ y1 x2))
          (list x1 (thing-x thing1) y1 (thing-y thing1)
                x2 (thing-x thing2) y2 (thing-y thing2))
          (setf (thing-x thing2) (list x1))
          (list x1 (thing-x thing1) y1 (thing-y thing1)
                x2 (thing-x thing2) y2 (thing-y thing2)))))
>>  Changing X from 7 to (9) in #<THING 43147374>.
=>  ((1 1 2 2 7 7 8 8)
     9
     (9 9 2 2 7 7 8 8) 
     (9)
     (9 9 2 2 (9) (9) 8 8))
```


## 影響

[`defclass`](7.7.defclass.html)


## 例外

もし*slot-name*が*instance*のスロット名ではない場合は、
結果は定義されていません。


## 参考

[`with-accessors`](7.7.with-accessors.html),
[`slot-value`](7.7.slot-value.html),
`symbol-macrolet`


## 備考

[`with-slots`](7.7.with-slots.html)の下記のフォームについて、

```lisp
(with-slots (slot-entry1 ... slot-entryn) instance-form form1 ... formk)
```

これは、下記の展開と同等です。

```lisp
(let ((in instance-form))
  (symbol-macrolet (Q1 ... Qn) form1 ... formk))
```

ただし、`Qi`は下記のようになります。

```lisp
(slot-entryi () (slot-value in 'slot-entryi))
```

もし*slot-entry_i*がシンボルなら次のようになります。

```lisp
(variable-namei () (slot-value in 'slot-namei))
```

もし*slot-entry_i*がフォームなら次のようになります。

```lisp
(variable-namei 'slot-namei)
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

