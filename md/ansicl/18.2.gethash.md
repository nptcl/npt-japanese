% Accessor GETHASH

[UP](18.2.html)  

---

# Accessor **GETHASH**


Accessor `GETHASH`


## 構文

`gethash` *key* *hash-table* `&optional` *default* => *value*, *present-p*  
(`setf` (`gethash` *key* *hash-table* `&optional` *default*) *new-value*)


## 引数と戻り値

*key* - オブジェクト  
*hash-table* - ハッシュテーブル  
*default* - オブジェクト。デフォルトは[`nil`](5.3.nil-variable.html)。  
*value* - オブジェクト  
*present-p* - generalized-boolean


## 定義

*value*は、*hash-table*の中にある、
キーが*key*と*hash-table*の等価テストで
等しいとされたもののオブジェクトです。
そのようなエントリーが存在しないときは、
*value*は*default*になります。

もしエントリーが見つかったときは*present-p*は*true*であり、
それ以外は*false*です。

[`setf`](5.3.setf.html)の[`gethash`](18.2.gethash.html)の使用は、
与えられたキーに関連付けられた値を修正するときか
あるいは新しいエントリーを追加するときに使用されます。
[`gethash`](18.2.gethash.html)フォームが[`setf`](5.3.setf.html)の`place`で使用された場合、
*default*が与えられたときは
通常の左から右への評価ルールに従って評価されますが、
その値は無視されます。


## 例文

```lisp
(setq table (make-hash-table)) =>  #<HASH-TABLE EQL 0/120 32206334>
(gethash 1 table) =>  NIL, false
(gethash 1 table 2) =>  2, false
(setf (gethash 1 table) "one") =>  "one"
(setf (gethash 2 table "two") "two") =>  "two"
(gethash 1 table) =>  "one", true
(gethash 2 table) =>  "two", true
(gethash nil table) =>  NIL, false
(setf (gethash nil table) nil) =>  NIL 
(gethash nil table) =>  NIL, true
(defvar *counters* (make-hash-table)) =>  *COUNTERS*
(gethash 'foo *counters*) =>  NIL, false
(gethash 'foo *counters* 0) =>  0, false
(defmacro how-many (obj) `(values (gethash ,obj *counters* 0))) =>  HOW-MANY
(defun count-it (obj) (incf (how-many obj))) =>  COUNT-IT
(dolist (x '(bar foo foo bar bar baz)) (count-it x))
(how-many 'foo) =>  2
(how-many 'bar) =>  3
(how-many 'quux) =>  0
```


## 副作用

なし。


## 影響

なし。


## 例外

なし。


## 参考

[`remhash`](18.2.remhash.html)


## 備考

第二返却値である*present-p*は、
エントリーの不在か、
*default*の値を持つエントリーの存在かを、
区別するときに使用できます。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

