% Function LDIFF, TAILP

[UP](14.2.html)  

---

# Function LDIFF, TAILP


Function `LDIFF`, `TAILP`


## 構文

`ldiff` *list* *object* => *result-list*  
`tailp` *object* *list* => *generalized-boolean*


## 引数と戻り値

*list* - リスト。ドットリストを受け付けます。  
*object* - オブジェクト  
*result-list* - リスト  
*generalized-boolean* - generalized-boolean


## 定義

*object*が*list*のどれかの末尾と等しいとき、
[`tailp`](14.2.ldiff.html)は*true*を返却し、それ以外は*false*を返却します。

*object*が*list*のどれかの末尾と等しいとき、
[`ldiff`](14.2.ldiff.html)は、*list*のリスト構造の中で、
*object*より前にある要素を
新しいリストとして返却します。
それ以外のときは、*list*のコピーを返却します。


## 例文

```lisp
(let ((lists '#((a b c) (a b c . d))))
  (dotimes (i (length lists)) ()
    (let ((list (aref lists i)))
      (format t "~2&list=~S ~21T(tailp object list)~
                 ~44T(ldiff list object)~%" list)
        (let ((objects (vector list (cddr list) (copy-list (cddr list))
                               '(f g h) '() 'd 'x)))
          (dotimes (j (length objects)) ()
            (let ((object (aref objects j)))
              (format t "~& object=~S ~21T~S ~44T~S"
                      object (tailp object list) (ldiff list object))))))))
>>  
>>  list=(A B C)         (tailp object list)    (ldiff list object)
>>   object=(A B C)      T                      NIL
>>   object=(C)          T                      (A B)
>>   object=(C)          NIL                    (A B C)
>>   object=(F G H)      NIL                    (A B C)
>>   object=NIL          T                      (A B C)
>>   object=D            NIL                    (A B C)
>>   object=X            NIL                    (A B C)
>>  
>>  list=(A B C . D)     (tailp object list)    (ldiff list object)
>>   object=(A B C . D)  T                      NIL
>>   object=(C . D)      T                      (A B)
>>   object=(C . D)      NIL                    (A B C . D)
>>   object=(F G H)      NIL                    (A B C . D)
>>   object=NIL          NIL                    (A B C . D)
>>   object=D            T                      (A B C)
>>   object=X            NIL                    (A B C . D)
=>  NIL
```


## 副作用

[`ldiff`](14.2.ldiff.html)と[`tailp`](14.2.ldiff.html)は、どちらも引数を修正しません。


## 影響

なし。


## 例外

*list*が通常のリストでもドットリストでもないときは、
型`type-error`のエラーを発生させる準備をする必要があります。


## 参考

[`set-difference`](14.2.set-difference.html)


## 備考

もし*list*が循環リストのとき、
*object*が実際に*list*の末尾である場合にのみ、
[`tailp`](14.2.ldiff.html)は確実に値を返却します。
その他の結果は未定義であり、
ある実装では循環を検出して*false*を返却しますが、
ある実装ではそのような状況を検出しませんので、
このような場合、[`tailp`](14.2.ldiff.html)は値を返却することなく、
ただ無限にループするかもしれません。

[`tailp`](14.2.ldiff.html)は次のように定義できます。

```lisp
(defun tailp (object list)
  (do ((list list (cdr list)))
      ((atom list) (eql list object))
     (if (eql object list)
         (return t))))
```

そして[`ldiff`](14.2.ldiff.html)は次のようになります。

```lisp
(defun ldiff (list object)
  (do ((list list (cdr list))
       (r '() (cons (car list) r)))
      ((atom list)
       (if (eql list object) (nreverse r) (nreconc r list)))
    (when (eql object list)
      (return (nreverse r)))))
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

