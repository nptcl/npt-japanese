% Function MAPC, MAPCAR, MAPCAN, MAPL, MAPLIST, MAPCON

[UP](14.2.html)  

---

# Function **MAPC, MAPCAR, MAPCAN, MAPL, MAPLIST, MAPCON**


Function `MAPC`, `MAPCAR`, `MAPCAN`, `MAPL`, `MAPLIST`, `MAPCON`

## 構文

`mapc` *function* &rest *lists+* => *list-1*  
`mapcar` *function* &rest *lists+* => *result-list*  
`mapcan` *function* &rest *lists+* => *concatenated-results*  
`mapl` *function* &rest *lists+* => *list-1*  
`maplist` *function* &rest *lists+* => *result-list*  
`mapcon` *function* &rest *lists+* => *concatenated-results*


## 引数と戻り値

*function* - *lists*と同じ数の引数を取る関数指定子  
*lists* - 通常のリスト  
*list-1* - 最初の*lists*（通常のリストである必要があります）  
*result-list* - リスト  
*concatenated-results* - リスト


## 定義

マッピング操作は、
引数の各列からひとつ得たものの集合を、
連続で関数に適用した結果を返却します。
[`mapc`](14.2.mapc.html)と[`mapl`](14.2.mapc.html)以外は、関数の返却値で構成された結果を返却します。
[`mapc`](14.2.mapc.html)と[`mapl`](14.2.mapc.html)の場合は、返却値は*lists*の列です。

*function*は、最初はインデックス0番目の全ての要素で呼び出され、
そのあとインデックス1番目の全ての要素で呼ばれ、
以下同様に実行します。
もし*function*がシンボルのときは、
それは[`symbol-function`](10.2.symbol-function.html)が実行されたかのように
関数へ[`coerce`](4.4.coerce.html)されます。

[`mapcar`](14.2.mapc.html)は、*lists*の連続した要素に対して操作します。
*function*は、各*lists*の最初の要素で適用し、
そのあと各*lists*の二番目の要素で適用し、
それを繰り返します。
繰り返しは最短の*lists*がなくなったときに終了し、
他の*lists*の余分な要素は無視されます。
[`mapcar`](14.2.mapc.html)の返却値は、
*function*が連続して呼び出された結果のリストです。

[`mapc`](14.2.mapc.html)は[`mapcar`](14.2.mapc.html)と似ていますが、
適用された*function*の結果を蓄積しない所が違っています。
*lists*引数が返却されます。

[`maplist`](14.2.mapc.html)は[`mapcar`](14.2.mapc.html)と似ていますが、
*function*は、*lists*のサブリストを連続して適用するところが違っています。
*function*は、最初は*lists*そのものに適用され、
そのあと各*lists*の`cdr`によって適用され、
さらに各*lists*の`cdr`の`cdr`によって適用され、
それをくり返します。

[`mapl`](14.2.mapc.html)は[`maplist`](14.2.mapc.html)と似ていますが、
適用された*function*の結果を蓄積しない所が違っています。
*list-1*が返却されます。

[`mapcan`](14.2.mapc.html)と[`mapcon`](14.2.mapc.html)は、
それぞれ[`mapcar`](14.2.mapc.html)と[`maplist`](14.2.mapc.html)に似ていますが、
適用した*function*の結果を、
[`list`](14.2.list-function.html)ではなく[`nconc`](14.2.nconc.html)によって
リストに結合するところが違っています。

これは次のような関係であり、

```lisp
(mapcon f x1 ... xn)
  ==  (apply #'nconc (maplist f x1 ... xn))
```

[`mapcan`](14.2.mapc.html)と[`mapcar`](14.2.mapc.html)の関係に似ています。


## Examples:

```lisp
(mapcar #'car '((1 a) (2 b) (3 c))) =>  (1 2 3) 
(mapcar #'abs '(3 -4 2 -5 -6)) =>  (3 4 2 5 6)
(mapcar #'cons '(a b c) '(1 2 3)) =>  ((A . 1) (B . 2) (C . 3))

(maplist #'append '(1 2 3 4) '(1 2) '(1 2 3)) 
=>  ((1 2 3 4 1 2 1 2 3) (2 3 4 2 2 3)) 
(maplist #'(lambda (x) (cons 'foo x)) '(a b c d))
=>  ((FOO A B C D) (FOO B C D) (FOO C D) (FOO D))
(maplist #'(lambda (x) (if (member (car x) (cdr x)) 0 1)) '(a b a c d b c))
=>  (0 0 1 0 1 1 1)
;もし入力のリストに対応する要素が
;そのリスト内で最後の要素の場合は1

(setq dummy nil) =>  NIL 
(mapc #'(lambda (&rest x) (setq dummy (append dummy x)))
       '(1 2 3 4)
       '(a b c d e)
       '(x y z)) =>  (1 2 3 4) 
dummy =>  (1 A X 2 B Y 3 C Z)                   

(setq dummy nil) =>  NIL 
(mapl #'(lambda (x) (push x dummy)) '(1 2 3 4)) =>  (1 2 3 4) 
dummy =>  ((4) (3 4) (2 3 4) (1 2 3 4)) 

(mapcan #'(lambda (x y) (if (null x) nil (list x y)))
         '(nil nil nil d e)
         '(1 2 3 4 5 6)) =>  (D 4 E 5) 
(mapcan #'(lambda (x) (and (numberp x) (list x)))
         '(a 1 b c 3 4 d 5))
=>  (1 3 4 5)
```

この場合は、関数はフィルタの役割を果たします。
これは標準のLispの語句の[`mapcan`](14.2.mapc.html)です。

```lisp
(mapcon #'list '(1 2 3 4)) =>  ((1 2 3 4) (2 3 4) (3 4) (4)) 
```


## 影響

なし。


## 例外

*lists*が通常のリストではないとき、
型[`type-error`](4.4.type-error.html)のエラーを通知する準備をしなければなりません。


## 参考

[`dolist`](6.2.dolist.html),
[`map`](17.3.map.html),
3.6. 横断の規則と副作用


## 備考

なし。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

