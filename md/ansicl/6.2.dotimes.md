% Macro DOTIMES

[UP](6.2.html)  

---

# Macro **DOTIMES**


Macro `DOTIMES`


## 構文

`dotimes` (*var* *count-form* [*result-form*]) *declaration\**
 `{`*tag* `|` *statement*`}`\*  
=> *result\**


## 引数と戻り値

*var* - シンボル  
*count-form* - フォーム  
*result-form* - フォーム  
*declaration* - 宣言式。評価されません。  
*tag* - `go`のタグ。評価されません。  
*statement* - compound-form。下記の説明に従って評価されます。  
*result* - もし[`return`](5.3.return.html)か[`return-from`](5.3.return-from.html)が実行されたときは、
そのフォームから渡された返却値であり、
それ以外の場合は*result-form*による返却値です。


## 定義

[`dotimes`](6.2.dotimes.html)は、一連の整数による繰り返しを行います。

[`dotimes`](6.2.dotimes.html)は*count-form*を評価し
その返却値は整数でなければなりません。
もし*count-form*がゼロか負の場合は、ボディ部は実行されません。
それから[`dotimes`](6.2.dotimes.html)はボディ部をいちど実行するごとに、
各整数値を0から*count-form*を含まない値まで上げて行き、
*tag*と*statement*を順番に実行し、
その際に*var*を各整数に束縛します。
そして*result-form*を評価します。
*result-form*が処理された時点で、
*var*にはボディ部が実行された回数が束縛されています。
*tag*は*statement*にラベル付けされます。

[`nil`](5.3.nil-variable.html)という名前の暗黙の`block`が、
[`dotimes`](6.2.dotimes.html)のフォーム全体を囲みます。
[`return`](5.3.return.html)文は、それ以上の繰り返しを行うことなく
ループを即座に終了させるときに使われ、
そのときの返却値は0や複数の値を指定できます。

繰り返しのボディ部は暗黙の`tagbody`であり、
これは[`go`](5.3.go.html)文の遷移先としてのタグを含みます。
宣言は繰り返しのボディ部の前に配置できます。

*var*の束縛のスコープは、*count-form*を含みませんが
*result-form*は含まれます。

[`dotimes`](6.2.dotimes.html)が各繰り返しごとに*var*の
新しい束縛を確立するかどうか、
あるいは*var*が一度束縛されてからその後の繰り返しで
代入されるのかどうかは
実装依存です。


## 例文

```lisp
(dotimes (temp-one 10 temp-one)) =>  10
(setq temp-two 0) =>  0
(dotimes (temp-one 10 t) (incf temp-two)) =>  T
temp-two =>  10
```

次に文字列を処理する[`dotimes`](6.2.dotimes.html)の使用例を示します

```lisp
;;; もし指定された文字列のサブシーケンスが回文ならTrue。
;;; （つまり前から読んでも後ろから読んでも同じとき）
(defun palindromep (string &optional
                          (start 0)
                          (end (length string)))
  (dotimes (k (floor (- end start) 2) t)
   (unless (char-equal (char string (+ start k))
                       (char string (- end k 1)))
     (return nil))))
(palindromep "Able was I ere I saw Elba") =>  T
(palindromep "A man, a plan, a canal--Panama!") =>  NIL
(remove-if-not #'alpha-char-p          ;Remove punctuation.
              "A man, a plan, a canal--Panama!")
=>  "AmanaplanacanalPanama"
(palindromep
 (remove-if-not #'alpha-char-p
               "A man, a plan, a canal--Panama!")) =>  T
(palindromep
 (remove-if-not
  #'alpha-char-p
  "Unremarkable was I ere I saw Elba Kramer, nu?")) =>  T
(palindromep
 (remove-if-not
  #'alpha-char-p
  "A man, a plan, a cat, a ham, a yak,
                 a yam, a hat, a canal--Panama!")) =>  T
```


## 副作用

なし。


## 影響

なし。


## 例外

なし。


## 参考

[`do`](6.2.do.html),
[`dolist`](6.2.dolist.html),
[`tagbody`](5.3.tagbody.html)


## 備考

[`go`](5.3.go.html)は、[`dotimes`](6.2.dotimes.html)のボディ部で
*tag*によってラベル付けされた文へ
遷移を制御するために使用されます。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

