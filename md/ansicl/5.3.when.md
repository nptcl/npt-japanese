% Macro WHEN, UNLESS

[UP](5.3.html)  

---

# Macro **WHEN, UNLESS**


Macro `WHEN`, `UNLESS`


## 構文

`when` *test-form* *form\** => *result\**  
`unless` *test-form* *form\** => *result\**


## 引数と戻り値

*test-form* - フォーム  
*form* - 暗黙のprogn  
*result* - *test-form*が*true*のときは
[`when`](5.3.when.html)フォーム内の*form*の多値。
*test-form*が*false*のときは
[`unless`](5.3.when.html)フォーム内の*form*の多値。
それ以外は[`nil`](5.3.nil-variable.html)。


## 定義

[`when`](5.3.when.html)と[`unless`](5.3.when.html)は、
*test-form*単体に依存したフォームの実行を行います。

[`when`](5.3.when.html)のフォーム内において、
*test-form*が*true*のときは、*form*が左から右の順番で評価され、
その*form*の多値が[`when`](5.3.when.html)フォームからの返却値になります。
一方、*test-form*が*false*のときは、*form*は評価されず
[`when`](5.3.when.html)フォームは[`nil`](5.3.nil-variable.html)を返却します。

[`unless`](5.3.when.html)のフォーム内において、
*test-form*が*false*のときは、*form*が左から右の順番で評価され、
その*form*の多値が[`unless`](5.3.when.html)フォームからの返却値になります。
一方、*test-form*が*true*のときは、*form*は評価されず
[`unless`](5.3.when.html)フォームは[`nil`](5.3.nil-variable.html)を返却します。


## 例文

```lisp
(when t 'hello) =>  HELLO
(unless t 'hello) =>  NIL
(when nil 'hello) =>  NIL
(unless nil 'hello) =>  HELLO
(when t) =>  NIL
(unless nil) =>  NIL
(when t (prin1 1) (prin1 2) (prin1 3))
>>  123
=>  3
(unless t (prin1 1) (prin1 2) (prin1 3)) =>  NIL
(when nil (prin1 1) (prin1 2) (prin1 3)) =>  NIL
(unless nil (prin1 1) (prin1 2) (prin1 3))
>>  123
=>  3
(let ((x 3))
  (list (when (oddp x) (incf x) (list x))
        (when (oddp x) (incf x) (list x))
        (unless (oddp x) (incf x) (list x))
        (unless (oddp x) (incf x) (list x))
        (if (oddp x) (incf x) (list x)) 
        (if (oddp x) (incf x) (list x)) 
        (if (not (oddp x)) (incf x) (list x)) 
        (if (not (oddp x)) (incf x) (list x))))
=>  ((4) NIL (5) NIL 6 (6) 7 (7))
```


## 副作用

なし。


## 影響

なし。


## 例外

なし。


## 参考

[`and`](5.3.and-macro.html),
[`cond`](5.3.cond.html),
[`if`](5.3.if.html),
[`or`](5.3.or-macro.html)


## 備考

```lisp
(when test {form}+) == (and test (progn {form}+))
(when test {form}+) == (cond (test {form}+))
(when test {form}+) == (if test (progn {form}+) nil)
(when test {form}+) == (unless (not test) {form}+)
(unless test {form}+) == (cond ((not test) {form}+))
(unless test {form}+) == (if test nil (progn {form}+))
(unless test {form}+) == (when (not test) {form}+)
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

