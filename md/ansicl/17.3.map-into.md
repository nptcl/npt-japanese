% Function MAP-INTO

[UP](17.3.html)  

---

# Function MAP-INTO


Function `MAP-INTO`


## 構文

`map-into` *result-sequence* *function* &rest *sequences* => *result-sequence*


## 引数と戻り値

*result-sequence* - 正常なシーケンス  
*function* - *sequences*にある複数の引数を取る関数の指定子  
*sequences* - 正常なシーケンス


## 定義

引数*sequences*の各要素を*function*に適用した結果を
*result-sequence*に含めるよう、破壊的に修正します。

*result-sequence*と*sequences*の各要素は、
それぞれリストか`vector`のどちらかになります。
もし*result-sequence*と*sequences*の各要素が、
全てかあるいはいくつかが同じ長さではないとき、
繰り返しはもっとも短いシーケンス
（*sequences*のどれかか、あるいは*result-sequence*）が
使い終わったときに終わります。
もし*result-sequence*がfill-pointerを持った`vector`のとき、
そのfill-pointerは、何度繰り返しを行うかを決めるときには無視され、
その後、fill-pointerに関数が適用された回数が設定されます。
もし*result-sequence*が、*sequences*の最も短いものより長いときは、
*result-sequence*の末尾の余分な要素は変更されずに残されます。
もし*result-sequence*が[`nil`](5.3.nil-variable.html)のときは、
[`nil`](5.3.nil-variable.html)は長さ`0`のシーケンスなので、
即座に[`nil`](5.3.nil-variable.html)が返却されます。

もし*function*に副作用があるなら、
最初にインデックス`0`の全ての要素で呼ばれ、
次にインデックス`1`の全ての要素で呼ばれ、
といったように数えることができます。


## 例文

```lisp
(setq a (list 1 2 3 4) b (list 10 10 10 10)) =>  (10 10 10 10)
(map-into a #'+ a b) =>  (11 12 13 14)
a =>  (11 12 13 14)
b =>  (10 10 10 10)
(setq k '(one two three)) =>  (ONE TWO THREE)
(map-into a #'cons k a) =>  ((ONE . 11) (TWO . 12) (THREE . 13) 14)
(map-into a #'gensym) =>  (#:G9090 #:G9091 #:G9092 #:G9093)
a =>  (#:G9090 #:G9091 #:G9092 #:G9093)
```


## 影響

なし。


## 例外

*result-sequence*が正常なシーケンスでないとき、
型`type-error`のエラーを通知する準備をしなければなりません。
*sequences*の各要素が正常なシーケンスでないとき、
型`type-error`のエラーを通知する準備をしなければなりません。


## 参考

なし。


## 備考

[`map-into`](17.3.map-into.html)と[`map`](17.3.map.html)の違うところは、
シーケンスを作成するのではなく
既存のシーケンスを変更するところです。
加えて、[`map-into`](17.3.map-into.html)は2つの引数のみで呼び出すことができますが、
それに対して[`map`](17.3.map.html)は最低でも3つの引数が必要です。

[`map-into`](17.3.map-into.html)は下記のように定義できます。

```lisp
(defun map-into (result-sequence function &rest sequences)
  (loop for index below (apply #'min 
                               (length result-sequence)
                               (mapcar #'length sequences))
        do (setf (elt result-sequence index)
                 (apply function
                        (mapcar #'(lambda (seq) (elt seq index))
                                sequences))))
  result-sequence)
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

