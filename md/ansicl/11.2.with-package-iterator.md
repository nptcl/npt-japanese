% Macro WITH-PACKAGE-ITERATOR

[UP](11.2.html)  

---

# Macro **WITH-PACKAGE-ITERATOR**


Macro `WITH-PACKAGE-ITERATOR`


## 構文

`with-package-iterator`
 (*name* *package-list-form* `&rest` *symbol-types*) *declaration\** *form\**  
 => *result\**


## 引数と戻り値

*name* - シンボル  
*package-list-form* - フォーム。いちど評価されて*package-list*を生成します。  
*package-list* - パッケージ指定子のリストの指定子  
*symbol-types* - 次のシンボルのうちの一つ、`:internal`, `:external`, `:inherited`。  
*declaration* - 宣言式。評価されません。  
*form* - 暗黙のprogn  
*result* - *form*の返却値


## 定義

*form*のボディ部のレキシカルスコープ内で
*name*が[`macrolet`](5.3.flet.html)によって定義され、
`(name)`が連続して呼び出されたときに、
*package-list*のパッケージからシンボルがひとつずつ返却されます。

パッケージが複数継承されていたもののシンボルが
一度ではなく複数にわたって返却されるかどうかは指定されていません。
返却されるシンボルの順番は、
*package-list*のパッケージの順番を反映する必要はありません。
もし*package-list*に複数の要素があったときは、
シンボルがひとつ返却されるか複数回返却されるかは指定されていません。

*symbol-types*はパッケージのアクセス可能なシンボルに対して、
次のような返却の制御を行います。

- `:internal`
  - シンボルはパッケージに現れるものですが、`export`ではありません。
- `:external`
  - シンボルはパッケージに現れるものと、`export`の両方です。
- `:inherited`
  - シンボルは`use`されているパッケージの外部シンボルですが、
    `shadow`はされていないものです。

*symbol-types*にひとつ以上の引数が指定されたときは、
シンボルのアクセスが指定された
*symbol-types*のうちのどれかにマッチするものが返却されます。
実装はこの構文に追加で認識可能なシンボルのアクセス型を
拡張するかもしれません。

`(name)`の起動は、下記のような4つの値を返却します。

1. シンボルが返却されたかどうかを示すフラグ（*true*ならシンボルが返却された）。
2. シンボルであり指定したパッケージのひとつからアクセスしたもの。
3. シンボルのアクセスの型である次のどれか`:internal`, `:external`, `:inherited`。
4. パッケージであり、シンボルが得られた場所。
パッケージは*package-list*内にパッケージとして現れたものか
あるいはその名前のひとつ。

`(name)`の連続の呼び出しによって
全てのシンボルが返却されたあとは、
ただひとつの値である[`nil`](5.3.nil-variable.html)のみが返却されます。

返却値の第2、第3、第4の意味は、
以下で示される第2返却値の内容で、
返却されたシンボルが返却されたパッケージでアクセス可能ということです。

- `:internal`
  - 現れたシンボルは`export`ではない。
- `:external`
  - 現れたシンボルは`export`されている。
- `:inherited`
  - シンボルは現れてない（したがって`shadow`でもない）が、
    `use`されたいくつかのパッケージから継承されている。

呼び出しフォームを囲った何かのクロージャーを返すなどして、
[`with-package-iterator`](11.2.with-package-iterator.html)フォームの動的エクステントの外で
繰り返しの暗黙の内部状態の何かが返却された場合には、
何が起こるは規定されていません。

[`with-package-iterator`](11.2.with-package-iterator.html)はいくつでもネストすることができ、
それらの全てのマクロが違う名前を持つ場合は、
もっとも内側のボディ部では
確立されたすべてのローカルマクロを起動することができます。


## 例文

下記の関数はどんなパッケージでも[`t`](5.3.t-variable.html)を返却し、
もし[`with-package-iterator`](11.2.with-package-iterator.html)の使用で
対応する[`do-symbols`](11.2.do-symbols.html)の使用が合意されなかったときは
エラーが発生します。

```lisp
(defun test-package-iterator (package)
  (unless (packagep package)
    (setq package (find-package package)))
  (let ((all-entries '())
        (generated-entries '()))
    (do-symbols (x package) 
      (multiple-value-bind (symbol accessibility) 
          (find-symbol (symbol-name x) package)
        (push (list symbol accessibility) all-entries)))
    (with-package-iterator (generator-fn package 
                            :internal :external :inherited)
      (loop     
        (multiple-value-bind (more? symbol accessibility pkg)
            (generator-fn)
          (unless more? (return))
          (let ((l (multiple-value-list (find-symbol (symbol-name symbol) 
                                                     package))))
            (unless (equal l (list symbol accessibility))
              (error "Symbol ~S not found as ~S in package ~A [~S]"
                     symbol accessibility (package-name package) l))
            (push l generated-entries)))))
    (unless (and (subsetp all-entries generated-entries :test #'equal)
                 (subsetp generated-entries all-entries :test #'equal))
     (error "Generated entries and Do-Symbols entries don't correspond"))
    t))
```

下記の関数は全ての現れるシンボルを印刷します（複数回の可能性があります）。

```lisp
(defun print-all-symbols () 
  (with-package-iterator (next-symbol (list-all-packages)
                          :internal :external)
    (loop
      (multiple-value-bind (more? symbol) (next-symbol)
        (if more? 
           (print symbol)
           (return))))))
```

## 副作用

なし。


## 影響

なし。


## 例外

*symbol-types*が指定されなかったか、
*symbol-types*のどれかが実装では認識できないものが指定されたときは、
[`with-package-iterator`](11.2.with-package-iterator.html)は型[`program-error`](5.3.program-error.html)のエラーを通知します。

[`with-package-iterator`](11.2.with-package-iterator.html)によって確立された
*name*という名前のローカル関数が、
その主値が*false*を返却したあとで
呼び出されたときの結果は未定義です


## 参考

3.6. 横断の規則と副作用


## 備考

なし。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

