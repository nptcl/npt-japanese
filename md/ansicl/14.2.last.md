% Function LAST

[UP](14.2.html)  

---

# Function LAST


Function `LAST`


## 構文

`last` *list* &optional *n* => *tail*


## 引数と戻り値

*list* - リスト。ドットリストは受け付けますが、循環リストは受け付けません。  
*n* - 非負の整数。デフォルトは`1`。  
*tail* - オブジェクト


## 定義

[`last`](14.2.last.html)は、*list*の最後の*n*個のコンス
（最後の*n*要素ではありません）を返却します
もし*list*が`()`なら、[`last`](14.2.last.html)は`()`を返却します。

もし*n*が`0`のとき、*list*を終端しているアトムが返却されます。
もし*n*が、*list*のコンスセルの数以上のとき、
*list*が返却されます。


## 例文

```lisp
(last nil) =>  NIL
(last '(1 2 3)) =>  (3)
(last '(1 2 . 3)) =>  (2 . 3)
(setq x (list 'a 'b 'c 'd)) =>  (A B C D)
(last x) =>  (D)
(rplacd (last x) (list 'e 'f)) x =>  (A B C D E F)
(last x) =>  (F)

(last '(a b c))   =>  (C)

(last '(a b c) 0) =>  ()
(last '(a b c) 1) =>  (C)
(last '(a b c) 2) =>  (B C)
(last '(a b c) 3) =>  (A B C)
(last '(a b c) 4) =>  (A B C)

(last '(a . b) 0) =>  B
(last '(a . b) 1) =>  (A . B)
(last '(a . b) 2) =>  (A . B)
```


## 副作用

なし。


## 影響

なし。


## 例外

*list*が循環リストのときの結果は未定義です。
*n*が非負の整数ではないとき、
型`type-error`のエラーが発生します。


## 参考

[`butlast`](14.2.butlast.html),
[`nth`](14.2.nth.html)


## 備考

下記のコードは、[`last`](14.2.last.html)の定義です。

```lisp
(defun last (list &optional (n 1))
  (check-type n (integer 0))
  (do ((l list (cdr l))
       (r list)
       (i 0 (+ i 1)))
      ((atom l) r)
    (if (>= i n) (pop r))))
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

