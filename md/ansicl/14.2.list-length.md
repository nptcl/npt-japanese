% Function LIST-LENGTH

[UP](14.2.html)  

---

# Function **LIST-LENGTH**


Function `LIST-LENGTH`


## 構文

`list-length` *list* => *length*


## 引数と戻り値

*list* - 通常のリストか循環リスト  
*length* - 非負の整数か、[`nil`](5.3.nil-variable.html)


## 定義

*list*が通常のリストなら、*list*の長さを返却します。
*list*が循環リストなら、[`nil`](5.3.nil-variable.html)を返却します。


## 例文

```lisp
(list-length '(a b c d)) =>  4
(list-length '(a (b c) d)) =>  3
(list-length '()) =>  0
(list-length nil) =>  0
(defun circular-list (&rest elements)
  (let ((cycle (copy-list elements))) 
    (nconc cycle cycle)))
(list-length (circular-list 'a 'b)) =>  NIL
(list-length (circular-list 'a)) =>  NIL
(list-length (circular-list)) =>  0
```


## 副作用

なし。


## 影響

なし。


## 例外

*list*が通常のリストでも循環リストでもないときは、
型`type-error`のエラーが発生します。


## 参考

[`length`](17.3.length.html)


## 備考

[`list-length`](14.2.list-length.html)は次のように実装できます。

```lisp
(defun list-length (x)  
  (do ((n 0 (+ n 2))           ;カウンター。
       (fast x (cddr fast))    ;Fastポインター: 2つ進める。
       (slow x (cdr slow)))    ;Slowポインター: 1つ進める。
      (nil)
    ;; もしfastポインターが終了なら、カウントを返却する。
    (when (endp fast) (return n))
    (when (endp (cdr fast)) (return (+ n 1)))
	;; もしfastポインターがslotポインターと同じなら
	;; 循環リストから抜け出せていない。
	;; （深い特性の話をすると：もし循環リストにはまったのなら
	;;   fastポインターはやがてslotポインターと等しくなるだろう。
	;;   このような事実がこの実装を正当化する。）
    (when (and (eq fast slow) (> n 0)) (return nil))))
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

