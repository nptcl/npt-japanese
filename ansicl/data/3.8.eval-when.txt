;;
;;  ANSI Common Lisp 日本語訳
;;  3. 評価とコンパイル
;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  Special Operator EVAL-WHEN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
@begin: 3.8.eval-when
@name: eval-when
@type: special-operator

Special Operator `EVAL-WHEN`


## {syntax}

`eval-when` (*situation\**) *form\** => *result\**


## {arguments-and-values}

*situation* - 次のシンボルのうちのひとつ、
`:compile-toplevel`, `:load-toplevel`, `:execute`,
`compile`, `load`, `eval`。

ただし`eval`, `compile`, `load`の使用は非推奨です。

*form* - {implicit-progn}  
*result* - もし*form*が実行されたときはその値を、それ以外は{nil!variable:link}。


## {description}

{eval-when:link}フォームのボディ部は、
*situations*のリスト下にあるときのみ{implicit-progn}として処理します。

{compile-file:link}で処理しているコード内で
{eval-when:link}が{top-level-form}として現れたとき、
*situation*に`:compile-toplevel`（あるいは`compile`）と
`:load-toplevel`（あるいは`load`）を使用することで
評価を行うかどうかを制御できます。
{3.2.3:ab}をご確認ください。

*situation*に`:execute`（あるいは`eval`）を使用することで
{eval-when:link}のその他のフォームの評価を行うかどうかを制御できます。
これは、それらが{top-level-form}のものではないか、
あるいはコードが{eval:link}か{compile:link}によって処理されたときです。
もし`:execute`の*situation*にそのようなフォームが指定されたとき、
*form*のボディ部は{implicit-progn}として処理され、
それ以外の場合は{eval-when:link}フォームは{nil!variable:link}を返却します。

{eval-when:link}は通常{top-level-form}に現れますが、
それが{non-top-level-form}として現れても意味があります。
しかし、{3.2:ab}に記載されているコンパイル時の副作用は、
ただ{eval-when:link}が{top-level-form}に現れたときのみ行われます。


## {examples}

{eval-when:link}のひとつの使用例として、
コンパイラーがファイルを適切に読み込めるように、
ユーザーが定義したリーダーマクロを使う方法を示します。
下記のように記載する必要があります。

```lisp
(eval-when (:compile-toplevel :load-toplevel :execute)
  (set-macro-character #\$ #'(lambda (stream char)
                               (declare (ignore char))
                               (list 'dollar (read stream))))) =>  T
```

これは{set-macro-character:link}の呼び出しが
コンパイラーの実行環境内での実行を引き起こし、
それによってリーダーの構文テーブルに修正が起こります。

```lisp
;;;     このケースではEVAL-WHENはtoplevelではなく、ただ:EXECUTEキーワード
;;;     のみが考慮されます。コンパイル時においてこれらの効果はありません。
;;;     ロード（もしこのLETがトップレベルのとき）か実行時（もしこのLETが
;;;     他のフォームに埋め込まれていて後にならないと呼び出されないとき）、
;;;     これは(SYMBOL-FUNCTION 'FOO1)に1を返却する関数がセットされます。
(let ((x 1))
  (eval-when (:execute :load-toplevel :compile-toplevel)
    (setf (symbol-function 'foo1) #'(lambda () x))))

;;;     もしこの式がコンパイル時にファイルのtoplevelとしてあらわれたとき、
;;;     これはコンパイル時とロードに(SYMBOL-FUNCTION 'FOO2)に2を返却する
;;;     関数をセットする効果があります。
(eval-when (:execute :load-toplevel :compile-toplevel)
  (let ((x 2))
    (eval-when (:execute :load-toplevel :compile-toplevel)
      (setf (symbol-function 'foo2) #'(lambda () x)))))

;;;     もしこの式がコンパイル時にファイルのtoplevelとしてあらわれたとき、
;;;     これはコンパイル時とロードに(SYMBOL-FUNCTION 'FOO2)に3を返却する
;;;     関数をセットする効果があります。
(eval-when (:execute :load-toplevel :compile-toplevel)
  (setf (symbol-function 'foo3) #'(lambda () 3)))

;;; #4: これはいつでも何もしません。単純にNILを返却します。
(eval-when (:compile-toplevel)
  (eval-when (:compile-toplevel) 
    (print 'foo4)))

;;;     もしこの式がコンパイル時にファイルのtoplevelとしてあらわれたとき、
;;;     コンパイル時にFOO5が印字されます。もしこのフォームが非toplevelの
;;;     位置に現れたとき、コンパイル時には何も印字されません。
;;;     文脈に関係なくロード時と実行時は何も印字されません。
(eval-when (:compile-toplevel) 
  (eval-when (:execute)
    (print 'foo5)))

;;;     もしこの式がコンパイル時にファイルのtoplevelとしてあらわれたとき、
;;;     コンパイル時にFOO6が印字されます。もしこのフォームが非toplevelの
;;;     位置に現れたとき、コンパイル時には何も印字されません。
;;;     文脈に関係なくロード時と実行時は何も印字されません。
(eval-when (:execute :load-toplevel)
  (eval-when (:compile-toplevel)
    (print 'foo6)))
```


## {affected-by}

なし。


## {exceptional-situations}

なし。


## {see-also}

{compile-file:link},
{3.2:ab}


## {notes}

{eval-when:link}の定義により、下記の論理的な効果が示されます。

- ひとつの{eval-when:link}式は、ボディ部が最大で一度実行されます。

- {top-level-form}で使用されたマクロは、
マクロ展開の中のフォームによって副作用が完了することによって出力されます。
そのマクロ展開自身には副作用は生じません。

次は悪い例。

```lisp
(defmacro foo ()
  (really-foo)
  `(really-foo))
```
次は良い例。

```lisp
(defmacro foo ()
  `(eval-when (:compile-toplevel :execute :load-toplevel) (really-foo)))
```

この規約を守ることで、マクロが{non-top-level-form}として現れたときに、
直感的に動作するようになります。

- {eval-when:link}の周りに配置された変数の束縛は確実にその束縛をに捕捉します。
なぜなら`compile-time-too`モードは生じないからです
（例えば、変数束縛の導入は、{eval-when:link}が
{top-level-form}ではないことを意味しているからです）。

例えば下記の文を考えます。

```lisp
(let ((x 3))
  (eval-when (:execute :load-toplevel :compile-toplevel) (print x)))
```

この文は、
実行時（例えばロード）に`3`を印字します。
そしてコンパイル時には何も印字しません。
{defun:link}と{defmacro:link}の展開は、
{eval-when:link}を実行することができて、
かつ正しく{lexical-environment}を捕捉することができるということは
重要なことです。

下記の文

```lisp
(defun bar (x) (defun foo () (+ x 3)))
```

これは、次のように展開されます。

```lisp
(defun bar (x) 
  (progn (eval-when (:compile-toplevel) 
           (compiler::notice-function-definition 'foo '(x)))
         (eval-when (:execute :load-toplevel)
           (setf (symbol-function 'foo) #'(lambda () (+ x 3))))))
```

この展開は、上記のルールに従うと次の同等になります。

```lisp
(defun bar (x) 
  (setf (symbol-function 'foo) #'(lambda () (+ x 3))))
```

これは`bar`の定義が{top-level-form}のものではありません。

@end

