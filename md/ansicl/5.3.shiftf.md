% Macro SHIFTF

[UP](5.3.html)  

---

# Macro **SHIFTF**


Macro `SHIFTF`


## 構文

`shiftf` *place*+ *newvalue* => *old-value-1*


## 引数と戻り値

*place* - `place`  
*newvalue* - フォーム。評価されます。  
*old-value-1* - オブジェクト（最初の`place`の古い値）


## 定義

[`shiftf`](5.3.shiftf.html)は、各要素の値の変更を行いますが、
まず*newvalue*を最後の`place`へ格納し、
そのあと2番目から最後までの`place`の値を残りの`place`にシフトします。

もし*newvalue*が格納変数より多い値を生成したときは、余分な値は無視されます。
もし*newvalue*が格納変数より少ない値しか生成しなかったときは、
値のないものは[`nil`](5.3.nil-variable.html)がセットされます。

フォームが`(shiftf place1 place2 ... placen newvalue)`のとき、
*place1*～*placen*までの値を読み込み保存され、
そして*newvalue*が評価され、合計で`n+1`個の値が用意されます。
`2`～`n+1`個目の値は、それぞれ*place1*～*placen*に格納されます。
それは全て*place*フォームがシフトレジスタであるかのように扱われます。
*newvalue*は左からシフトされ、全ての値は左へひとつずつシフトされ、
そしてシフトから外された*place1*値が返却されます。

`place`のサブフォームの評価については、5.1.1.1. `place`のサブフォームの評価をご確認ください。


## 例文

```lisp
(setq x (list 1 2 3) y 'trash) =>  TRASH
(shiftf y x (cdr x) '(hi there)) =>  TRASH
x =>  (2 3)
y =>  (1 HI THERE)

(setq x (list 'a 'b 'c)) =>  (A B C)
(shiftf (cadr x) 'z) =>  B
x =>  (A Z C)
(shiftf (cadr x) (cddr x) 'q) =>  Z
x =>  (A (C) . Q)
(setq n 0) =>  0
(setq x (list 'a 'b 'c 'd)) =>  (A B C D)
(shiftf (nth (setq n (+ n 1)) x) 'z) =>  B
x =>  (A Z C D)
```


## 影響

[`define-setf-expander`](5.3.define-setf-expander.html),
[`defsetf`](5.3.defsetf.html),
[`*macroexpand-hook*`](3.8.macroexpand-hook.html)


## 例外

なし。


## 参考

[`setf`](5.3.setf.html),
[`rotatef`](5.3.rotatef.html),
5.1. 一般化された参照


## 備考

`(shiftf place1 place2 ... placen newvalue)`の効果は、だいたい次と同等です。

```lisp
(let ((var1 place1)
      (var2 place2)
      ...
      (varn placen)
      (var0 newvalue))
  (setf place1 var2)
  (setf place2 var3)
  ...
  (setf placen var0)
  var1)
```

ただし、これは各`place`のサブフォームを2度評価していますが、
[`shiftf`](5.3.shiftf.html)はただ一度だけです。
例えば、下記の通り。

```lisp
(setq n 0) =>  0
(setq x (list 'a 'b 'c 'd)) =>  (A B C D)
(prog1 (nth (setq n (+ n 1)) x)
       (setf (nth (setq n (+ n 1)) x) 'z)) =>  B
x =>  (A B Z D)
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

