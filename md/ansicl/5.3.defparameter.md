% Macro DEFPARAMETER, DEFVAR

[UP](5.3.html)  

---

# Macro **DEFPARAMETER, DEFVAR**


Macro `DEFPARAMETER`


## 構文

`defparameter` *name* *initial-value* [*documentation*] => *name*  
`defvar` *name* [*initial-value* [*documentation*]] => *name*


## 引数と戻り値

*name* - シンボル、評価されません。  
*initial-value* - フォーム。
[`defparameter`](5.3.defparameter.html)は常に評価されます。
しかし[`defvar`](5.3.defparameter.html)は*name*が`bound`ではないときのみ評価されます。  
*documentation* - 文字列、評価されません。


## 定義

[`defparameter`](5.3.defparameter.html)と[`defvar`](5.3.defparameter.html)は、*name*を動的変数として確立します。

[`defparameter`](5.3.defparameter.html)は、*name*という動的変数に*initial-value*を
無条件に割り当てます。
対称的に[`defvar`](5.3.defparameter.html)は
*name*という変数が`bound`ではない場合において、
与えられた*initial-value*の値を割り当てます。

*initial-value*が与えられなかったときは、
[`defvar`](5.3.defparameter.html)は*name*の動的変数に手を加えず
値のセルをそのままにします。
もし*name*が前に`bound`のときは古い値のままにしますし、
以前の値が`unbound`であったときは`unbound`のままにします。

*documentation*が与えられたときは、
*name*に対してドキュメント文字を種別[`variable`](25.2.documentation.html)として割り当てます。

[`defparameter`](5.3.defparameter.html)と[`defvar`](5.3.defparameter.html)は通常トップレベルフォームに現れますが、
非トップレベルフォームに現れても意味があります。
しかし、下記で説明するコンパイル時の副作用は、
[`defconstant`](5.3.defconstant.html)がトップレベルフォームとして現れた場所でのみ
効果があります。


## 例文

```lisp
(defparameter *p* 1) =>  *P*
*p* =>  1
(constantp '*p*) =>  false
(setq *p* 2) =>  2
(defparameter *p* 3) =>  *P*
*p* =>  3

(defvar *v* 1) =>  *V*
*v* =>  1
(constantp '*v*) =>  false
(setq *v* 2) =>  2
(defvar *v* 3) =>  *V*
*v* =>  2

(defun foo ()
  (let ((*p* 'p) (*v* 'v))
    (bar))) =>  FOO
(defun bar () (list *p* *v*)) =>  BAR
(foo) =>  (P V)
```

[`defparameter`](5.3.defparameter.html)と[`defvar`](5.3.defparameter.html)の主な操作上の違いは、
[`defparameter`](5.3.defparameter.html)は*name*に無条件にアサインするのに対して、
[`defvar`](5.3.defparameter.html)は条件付きでそれを行うことにあります。
実際にこれらは
[`defparameter`](5.3.defparameter.html)はロードやリロード時に
定義で新しい値を変数に設定するときに便利ですし、
[`defvar`](5.3.defparameter.html)の場合はファイルのロードやリロード時に
古い値をそのままにしておきたいときに便利です。
例として、次のような内容のファイルを示します。

```lisp
(defvar *the-interesting-numbers* '())
(defmacro define-interesting-number (name n)
  `(progn (defvar ,name ,n)
          (pushnew ,name *the-interesting-numbers*)
          ',name))
(define-interesting-number *my-height* 168) ;cm
(define-interesting-number *my-weight* 13)  ;stones
```

この`*the-interesting-numbers*`の初期値`()`はただの種であり、
一度そこから何かを育てたあとは
他の何かリセットすることはないでしょう。
そのため、[`defvar`](5.3.defparameter.html)を使うことで、
ファイルを二回読み込んだときに
`*interesting-numbers*`の情報が
リセットされないようにしています。
確かに`define-interesting-number`の
二回の呼び出しは処理されますが、
しかし他のファイルで追加の呼び出しがあったときは
その情報が損失することはありません。
一方、次のコードを見てみます。

```lisp
(defparameter *default-beep-count* 3)
(defun beep (&optional (n *default-beep-count*))
  (dotimes (i n) (si:%beep 1000. 100000.) (sleep 0.1)))
```

ここでは、コードを編集して`*default-beep-count*`の初期値を変更し、
ファイルをリロードして新しい値を取得する
ということを簡単に想像できます。
値の更新を簡単にするために
[`defparameter`](5.3.defparameter.html)を使用しました。

一方で、このような状況で[`defvar`](5.3.defparameter.html)を使うことは潜在的な価値があります。
例えば、誰かが事前に`*default-beep-count*`の違う値を定義していたとします。
あるいは、ファイルを読み込んでからその値を手動で変更したとします。
どちらの場合でも、[`defparameter`](5.3.defparameter.html)のかわりに[`defvar`](5.3.defparameter.html)を使用していれば、
ファイルのロード・リロードを行っても
それらのユーザーの好みは上書きされることはありません。

[`defvar`](5.3.defparameter.html)と[`defparameter`](5.3.defparameter.html)のどちらを選んでも、
プログラムには目に見える結果をもたらしますが、
それにもかかわらず主観的な理由で選択されることがあります。


## 副作用

もし[`defvar`](5.3.defparameter.html)か[`defparameter`](5.3.defparameter.html)が
トップレベルフォームとして現れたときは、
コンパイラーは*name*を`special`として`proclaim`されるよう
認識しなければなりません。
しかし、コンパイル時に*initial-value*フォームが評価されたり、
*name*という動的変数に代入してはいけません。

適合するプログラムの正しい動作を妨げない限り、
コンパイル時または実行時に、
追加の（実装で定義された）副作用があってもかまいません。


## 影響

[`defvar`](5.3.defparameter.html)は、*name*が`bound`であるかどうかに影響されます。


## 例外

なし。


## 参考

`declaim`,
[`defconstant`](5.3.defconstant.html),
[`documentation`](25.2.documentation.html),
[3.2. コンパイル](3.2.html)


## 備考

慣習として動的変数の名前には
始まりと終わりにアスタリスクを付けたものが使われます。
例えば`*foo*`は良い動的変数の名前ですが、
レキシカル変数には使われません。
`foo`であれば良いレキシカル変数の名前になりますが、
動的変数には使われません。
この命名規則は、Common Lisp内に定義されたすべての名前で見られますが、
適合するプログラムも適合する実装もこの規則を遵守する義務はありません。

追加で副作用が許可される理由は、
実装がその定義に付随する通常の「帳簿管理」を行えるようにすることです。
例えば、[`defvar`](5.3.defparameter.html)と[`defparameter`](5.3.defparameter.html)フォームのマクロ展開には、
定義が記載されているソースファイルの名前を記録するためにアレンジした
コードが含まれるかもしれません。

[`defparameter`](5.3.defparameter.html)と[`defvar`](5.3.defparameter.html)は下記のように定義されます。

```lisp
(defmacro defparameter (name initial-value 
                        &optional (documentation nil documentation-p))
  `(progn (declaim (special ,name))
          (setf (symbol-value ',name) ,initial-value)
          ,(when documentation-p
             `(setf (documentation ',name 'variable) ',documentation))
          ',name))
(defmacro defvar (name &optional
                       (initial-value nil initial-value-p)
                       (documentation nil documentation-p))
  `(progn (declaim (special ,name))
          ,(when initial-value-p
             `(unless (boundp ',name)
                (setf (symbol-value ',name) ,initial-value)))
          ,(when documentation-p
             `(setf (documentation ',name 'variable) ',documentation))
          ',name))
```


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

