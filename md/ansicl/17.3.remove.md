% Function REMOVE, REMOVE-IF, REMOVE-IF-NOT, DELETE, DELETE-IF, DELETE-IF-NOT

[UP](17.3.html)  

---

# Function **REMOVE, REMOVE-IF, REMOVE-IF-NOT, DELETE, DELETE-IF, DELETE-IF-NOT**


Function `REMOVE`


## 構文

`remove` *item* *sequence*
 &key *from-end* *test* *test-not* *start* *end* *count* *key*
 => *result-sequence*  
`remove-if` *predicate* *sequence*
 &key *from-end* *start* *end* *count* *key*
 => *result-sequence*  
`remove-if-not` *predicate* *sequence*
 &key *from-end* *start* *end* *count* *key*
 => *result-sequence*  
`delete` *item* *sequence*
 &key *from-end* *test* *test-not* *start* *end* *count* *key*
 => *result-sequence*  
`delete-if` *predicate* *sequence*
 &key *from-end* *start* *end* *count* *key*
 => *result-sequence*  
`delete-if-not` *predicate* *sequence*
 &key *from-end* *start* *end* *count* *key*
 => *result-sequence*  


## 引数と戻り値

*item* - オブジェクト  

*sequence* - 正常なシーケンス  
*predicate* - 1つの引数をとりgeneralized-booleanを返却する関数の指定子  
*from-end* - generalized-boolean、デフォルトは*false*  
*test* - 2つの引数を取りgeneralized-booleanを返却する関数の指定子  
*test-not* - 2つの引数を取りgeneralized-booleanを返却する関数の指定子  
*start*, *end* - *sequence*の境界インデックス指定子。
デフォルトは*start*, *end*それぞれ`0`と[`nil`](5.3.nil-variable.html)。  
*count* - 整数か[`nil`](5.3.nil-variable.html)。デフォルトは[`nil`](5.3.nil-variable.html)。  
*key* - 1つの引数を取る関数の指定子、または[`nil`](5.3.nil-variable.html)  
*result-sequence* - シーケンス


## 定義

[`remove`](17.3.remove.html)、[`remove-if`](17.3.remove.html)、[`remove-if-not`](17.3.remove.html)は、
*sequence*からテストを満たした要素を削除したものを返却します。

[`delete`](17.3.remove.html)、[`delete-if`](17.3.remove.html)、[`delete-if-not`](17.3.remove.html)は、
それぞれ[`remove`](17.3.remove.html)、[`remove-if`](17.3.remove.html)、[`remove-if-not`](17.3.remove.html)に
似ていますが、これらは*sequence*を修正します。

もし*sequence*が`vector`のとき、返却値は`vector`であり、
*sequence*と同じ実際のarray-element-typeを持ちます。
もし*sequence*がリストなら、返却値はリストです。

*from-end*が*true*のときは、
ただ*count*が指定されたときが重要になります。
このような場合、右端から（左端ではなく）テストを満たす
*count*の要素だけが削除されます。

*count*が与えられたとき、削除する要素数を制限できます。
もしテストを満たす要素が*count*を越えていたときは、
それらの要素は左端か右端（*from-end*の値による）から、
ただ*count*で指定された数だけ削除されます。
もし*count*負の値が指定されたときは、
`0`が指定されたかのようにふるまいます。
もし*count*が[`nil`](5.3.nil-variable.html)のときは、
*item*にマッチした全てに影響します。

これらの全ての関数は、削除されなかった要素は、
結果内に*sequence*と同じ順序で現れます。

[`remove`](17.3.remove.html)、[`remove-if`](17.3.remove.html)、[`remove-if-not`](17.3.remove.html)は、
*sequence*と同じ型をもつシーケンスであり、
*start*, *end*に囲まれたサブシーケンスのテストを満たし
削除されたもの以外は同じ要素を返却します。
これは非破壊的な操作です。
もし要素の削除が必要になった時は、コピーが返却されます。
[`remove`](17.3.remove.html)の返却値は、*sequence*を共有するかもしれません。
もし要素に削除が生じなかったときは、
入力の*sequence*と同一のものが返却される可能性があります。

[`delete`](17.3.remove.html)、[`delete-if`](17.3.remove.html)、[`delete-if-not`](17.3.remove.html)は、
*sequence*と同じ型をもつシーケンスであり、
*start*, *end*に囲まれたサブシーケンスのテストを満たし
削除されたもの以外は同じ要素を返却します。
*sequence*は返却値の構築のために破壊されるかもしれません。
しかし、返却値は*sequence*と同一の場合もあれば、
そうではない場合もあります。

[`delete`](17.3.remove.html)は、*sequence*がリストのときは、
*sequence*のトップレベルのリスト構造の
どの[`car`](14.2.car.html), [`cdr`](14.2.car.html)でも[`setf`](5.3.setf.html)することが許されています。
もし*sequence*が`vector`のときは、
[`delete`](17.3.remove.html)は`vector`の次元を変更し、
その要素を並べ替えをせず、新しい位置にスライドして
返却値の`vector`を構築することが許されています。

delete-ifは以下のように正確に動作するように制約されています。

```lisp
 (delete nil sequence
             :test #'(lambda (ignore item) (funcall test item))
             ...)
```


## 例文

```lisp
(remove 4 '(1 3 4 5 9)) =>  (1 3 5 9)
(remove 4 '(1 2 4 1 3 4 5)) =>  (1 2 1 3 5)
(remove 4 '(1 2 4 1 3 4 5) :count 1) =>  (1 2 1 3 4 5)
(remove 4 '(1 2 4 1 3 4 5) :count 1 :from-end t) =>  (1 2 4 1 3 5)
(remove 3 '(1 2 4 1 3 4 5) :test #'>) =>  (4 3 4 5)
(setq lst '(list of four elements)) =>  (LIST OF FOUR ELEMENTS)
(setq lst2 (copy-seq lst)) =>  (LIST OF FOUR ELEMENTS)
(setq lst3 (delete 'four lst)) =>  (LIST OF ELEMENTS)
(equal lst lst2) =>  false
(remove-if #'oddp '(1 2 4 1 3 4 5)) =>  (2 4 4)
(remove-if #'evenp '(1 2 4 1 3 4 5) :count 1 :from-end t) 
=>  (1 2 4 1 3 5)
(remove-if-not #'evenp '(1 2 3 4 5 6 7 8 9) :count 2 :from-end t)
=>  (1 2 3 4 5 6 8)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete 4 tester) =>  (1 2 1 3 5)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete 4 tester :count 1) =>  (1 2 1 3 4 5)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete 4 tester :count 1 :from-end t) =>  (1 2 4 1 3 5)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete 3 tester :test #'>) =>  (4 3 4 5)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete-if #'oddp tester) =>  (2 4 4)
(setq tester (list 1 2 4 1 3 4 5)) =>  (1 2 4 1 3 4 5)
(delete-if #'evenp tester :count 1 :from-end t) =>  (1 2 4 1 3 5)    
(setq tester (list 1 2 3 4 5 6)) =>  (1 2 3 4 5 6) 
(delete-if #'evenp tester) =>  (1 3 5) 
tester =>  implementation-dependent
(setq foo (list 'a 'b 'c)) =>  (A B C)
(setq bar (cdr foo)) =>  (B C)
(setq foo (delete 'b foo)) =>  (A C)
bar =>  ((C)) or ...
(eq (cdr foo) (car bar)) =>  T or ...
```


## 副作用

[`delete`](17.3.remove.html)、[`delete-if`](17.3.remove.html)、[`delete-if-not`](17.3.remove.html)の場合、
*sequence*は返却値の構築のために破壊されるかもしれません。


## 影響

なし。


## 例外

*sequence*が正常なシーケンスでないとき、
型`type-error`のエラーを通知する準備をしなければなりません。


## 参考

3.2.1. コンパイラーの用語,
3.6. 横断の規則と副作用


## 備考

もし*sequence*が`vector`なら、
返却値は`simple`かもしれませんし、そうでないかもしれません。
また、返却値は*sequence*と同一かもしれませんし、
そうでないかもしれません。

`:test-not`パラメーターは非推奨です。

関数[`delete-if-not`](17.3.remove.html)と[`remove-if-not`](17.3.remove.html)は非推奨です。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

