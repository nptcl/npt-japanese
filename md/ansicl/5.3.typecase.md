% Macro TYPECASE, CTYPECASE, ETYPECASE

[UP](5.3.html)  

---

# Macro **TYPECASE, CTYPECASE, ETYPECASE**


Function `TYPECASE`, `CTYPECASE`, `ETYPECASE`


## 構文

`typecase` *keyform* `{`*normal-clause*`}`\* [*otherwise-clause*] => *result\**  
`ctypecase` *keyplace* `{`*normal-clause*`}`\* => *result\**  
`etypecase` *keyform* `{`*normal-clause*`}`\* => *result\**

```
normal-clause ::= (type form*) 
otherwise-clause ::= ({otherwise | t} form*) 
clause ::= normal-clause | otherwise-clause 
```


## 引数と戻り値

*keyform* - フォーム。評価され*test-key*を返却します。  
*keyplace* - フォーム。評価されて最初に*test-key*を返却します。  
*test-key* - *keyform*か*keyplace*が評価されることによって返却されるオブジェクト。  
*type* - 型指定子  
*form* - 暗黙のprogn  
*result* - マッチした*clause*の*form*の返却値


## 定義

これらのマクロは、*test-key*の型のマッチによって選択された
*clause*内の*form*のボディを条件付きで実行します。

*keyform*と*keyplace*が評価されて、*test-key*が生成されます。

そのあと各*normal-clause*が順番に考慮されます。
もし*test-key*が*clause*のどれかの型と同じであれば、
その*clause*内の*form*が暗黙のprognとして評価され、
[`typecase`](5.3.typecase.html)、[`ctypecase`](5.3.typecase.html)、[`etypecase`](5.3.typecase.html)フォームの返却値として
その多値が返却されます。

これらのマクロは、もし*normal-clause*がマッチしなかったときにだけ、
動作が違っています。
違いを下記に示します。

- [`typecase`](5.3.typecase.html)
  - もしマッチする*normal-clause*が存在せず*otherwise-clause*があるときは、
    自動的に*otherwise-clause*がマッチします。
    その*clause*にある*form*は暗黙のprognとして評価され、
    その*clause*内の*form*は暗黙のprognとして評価され、
    [`typecase`](5.3.typecase.html)フォームの返却値としてその多値が返却されます。
    <br><br>
    もし*otherwise-clause*がないときは、
    [`typecase`](5.3.typecase.html)は[`nil`](5.3.nil-variable.html)を返却します。
    <br><br>

- [`ctypecase`](5.3.typecase.html)
  - もしマッチする*normal-clause*が存在しないときは、
    型`type-error`の修正可能なエラーが発生します。
    問題の`datum`は*test-key*であり、
    `expected type`は`(member key1 key2 ...)`と同等の型です。
    `store-value` `restart`をエラーの修正に使うことができます。
    <br><br>
    もし`store-value` `restart`が起動されたら、
    その引数は新しい*test-key*であり、
    *keyplace*への保存は`(setf keyplace test-key)`のように行われます。
    それから[`ctypecase`](5.3.typecase.html)は最初から始められ、
    各*clause*が新しく評価されます。
    <br><br>
    もし`store-value` `restart`が対話形式で起動されたら、
    ユーザーは新しい*test-key*を使用できるような
    プロンプトが表示されます。
    <br><br>
    いずれのケースも成立しない場合、
    *keyplace*のサブフォームは再度評価されるかもしれません。
    <br><br>

- [`etypecase`](5.3.typecase.html)
  - もしマッチする*normal-clause*が存在しないときは、
    型`type-error`の修正不可能なエラーが発生します。
    問題の`datum`は*test-key*であり、
    `expected type`は`(member key1 key2 ...)`と同等の型です。
    <br><br>
    [`ctypecase`](5.3.typecase.html)とは対照的に[`etypecase`](5.3.typecase.html)の使用者は、
    もし*normal-clause*がマッチしなかったら
    [`etypecase`](5.3.typecase.html)は戻ってこないという事実に頼っています。
    <br><br>

3つの全ての場合において、
指定した型にマッチする*clause*が複数あっても許されます。
特にその型が何かのサブタイプになっているような場合も同様です。
そのような場合は、もっとも早い適切な*clause*が選ばれます。


## 例文

```lisp
;;; (この例で使われているTYPE-OFの部分は
;;;  実装依存であることに注意して下さい)
(defun what-is-it (x)
  (format t "~&~S is ~A.~%"
          x (typecase x
              (float "a float")
              (null "a symbol, boolean false, or the empty list")
              (list "a list")
              (t (format nil "a(n) ~(~A~)" (type-of x))))))
=>  WHAT-IS-IT
(map 'nil #'what-is-it '(nil (a b) 7.0 7 box))
>>  NIL is a symbol, boolean false, or the empty list.
>>  (A B) is a list.
>>  7.0 is a float.
>>  7 is a(n) integer.
>>  BOX is a(n) symbol.
=>  NIL
(setq x 1/3)
=>  1/3
(ctypecase x
    (integer (* x 4))
    (symbol  (symbol-value x)))
>>  Error: The value of X, 1/3, is neither an integer nor a symbol.
>>  To continue, type :CONTINUE followed by an option number:
>>   1: Specify a value to use instead.
>>   2: Return to Lisp Toplevel.
>>  Debug> :CONTINUE 1
>>  Use value: 3.7
>>  Error: The value of X, 3.7, is neither an integer nor a symbol.
>>  To continue, type :CONTINUE followed by an option number:
>>   1: Specify a value to use instead.
>>   2: Return to Lisp Toplevel.
>>  Debug> :CONTINUE 1
>>  Use value: 12
=>  48
x =>  12
```


## 影響

[`ctypecase`](5.3.typecase.html)と[`etypecase`](5.3.typecase.html)がエラーを通知したときは、
既存の`handler`と[`*debug-io*`](21.2.debug-io.html)に影響を及ぼす可能性があります。


## 例外

*normal-clause*にマッチしなかったときは、
[`ccase`](5.3.case.html)と[`ecase`](5.3.case.html)は
型`type-error`のエラーが発生します。

もしある*clause*がそれより早い*clause*によって完全にシャドウされ、
決して選択されないような状況であったとき、
コンパイラーは、型`style-warning`の警告を出すかどうか
選択することができます。


## 参考

[`case`](5.3.case.html),
[`cond`](5.3.cond.html),
[`setf`](5.3.setf.html),
5.1. 一般化された参照


## 備考

```lisp
(typecase test-key
  {(type form*)}*)
== 
(let ((#1=#:g0001 test-key))
  (cond {((typep #1# 'type) form*)}*))
```

[`etypecase`](5.3.typecase.html)と[`ctypecase`](5.3.typecase.html)によって使われる特定のエラーメッセージは、
実装によって変わります。
そんな中で、エラーメッセージの特定の単語の制御が必要なときは、
[`typecase`](5.3.typecase.html)の*otherwise-clause*で
明示的に適切なエラーメッセージを発生させるのが良いでしょう。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

