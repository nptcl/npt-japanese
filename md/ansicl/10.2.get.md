% Accessor GET

[UP](10.2.html)  

---

# Accessor **GET**


Accessor `GET`


## 構文

`get` *symbol* *indicator* &optional *default* => *value*  
(`setf` (`get` *symbol* *indicator* &optional *default*) *new-value*)


## 引数と戻り値

*symbol* - シンボル  
*indicator* - オブジェクト  
*default* - オブジェクト。デフォルトは[`nil`](5.3.nil-variable.html)。  
*value* - もしプロパティが存在していたらその値のオブジェクトを、
それ以外の場合は*default*。  
*new-value* - オブジェクト


## 定義

[`get`](10.2.get.html)は、*symbol*のプロパティリストから
*indicator*と同一の標識のプロパティを探し、
対応するプロパティの値を返却します。
標識に対して複数のプロパティがあるときは、
[`get`](10.2.get.html)は最初のプロパティを使います。
標識に対するプロパティが存在しないときは、
*default*が返却されます。

[`get`](10.2.get.html)の`setf`は、*symbol*のプロパティリストに、
すでに標識が存在するときは、
新しいオブジェクトを関連付けます。
もし存在しなかったときは、新しい関連を作成します。
標識に対して複数のプロパティがあるときは、
[`get`](10.2.get.html)の`setf`は最初のプロパティに*new-value*を関連付けます。
[`get`](10.2.get.html)フォームを`setf`の`place`で使うとき、
引数*default*が通常の左から右へ評価されるルールに従って評価され、
*value*は無視されます。


## 例文

```lisp
(defun make-person (first-name last-name)
  (let ((person (gensym "PERSON")))
    (setf (get person 'first-name) first-name)
    (setf (get person 'last-name) last-name)
    person)) =>  MAKE-PERSON
(defvar *john* (make-person "John" "Dow")) =>  *JOHN*
*john* =>  #:PERSON4603
(defvar *sally* (make-person "Sally" "Jones")) =>  *SALLY*
(get *john* 'first-name) =>  "John"
(get *sally* 'last-name) =>  "Jones"
(defun marry (man woman married-name)
  (setf (get man 'wife) woman)
  (setf (get woman 'husband) man)
  (setf (get man 'last-name) married-name)
  (setf (get woman 'last-name) married-name)
  married-name) =>  MARRY
(marry *john* *sally* "Dow-Jones") =>  "Dow-Jones"
(get *john* 'last-name) =>  "Dow-Jones"
(get (get *john* 'wife) 'first-name) =>  "Sally"
(symbol-plist *john*)
>  (WIFE #:PERSON4604 LAST-NAME "Dow-Jones" FIRST-NAME "John")
(defmacro age (person &optional (default ''thirty-something)) 
  `(get ,person 'age ,default)) =>  AGE
(age *john*) =>  THIRTY-SOMETHING
(age *john* 20) =>  20
(setf (age *john*) 25) =>  25
(age *john*) =>  25
(age *john* 20) =>  25
```


## 副作用

なし。


## 影響

なし。


## 例外

*symbol*がシンボルではないとき、
型`type-error`のエラーが発生します。


## 参考

[`getf`](14.2.getf.html),
[`symbol-plist`](10.2.symbol-plist.html),
[`remprop`](10.2.remprop.html)


## 備考

```lisp
(get x y) ==  (getf (symbol-plist x) y)
```

[`get`](10.2.get.html)は*indicator*のテストに
[`eql`](5.3.eql-function.html)ではなく[`eq`](5.3.eq.html)を用いるため、
移植しやすいコードとして、
*indicator*に数と文字を使うことは推奨されません。
そのため、そのような*indicator*を使ったときの効果は
実装依存です。

[`get`](10.2.get.html)を使用して、存在しないプロパティと
値がデフォルトであるプロパティを区別する方法はありません。
しかし、[`get-properties`](14.2.get-properties.html)を利用できます。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

