% Function APPLY

[UP](5.3.html)  

---

# Function **APPLY**


Function `APPLY`


## 構文

`apply` *function* &rest *args+* => *result\**


## 引数と戻り値

*function* - 関数指定子  
*args* - 広げられる引数リスト指定子  
*result* - *function*の返却値


## 定義

*function*を*args*で実行します。

*function*が`&rest`経由で引数を受け取るとき、
実装は`rest`パラメーターに、
[`apply`](5.3.apply.html)の最後の引数で共有されている構造のオブジェクトを、
束縛することが許されています
（ただし要求されているわけではありません）。
関数はそれが[`apply`](5.3.apply.html)経由か、
あるいは[`apply`](5.3.apply.html)の最後の引数が定数で呼び出されかどうかを
検出できないため、
適合するプログラムは、
`rest`のリスト構造が新しいコンスであることに頼ってはいけませんし、
またそのリスト構造を修正してもいけません。


## 例文

```lisp
(setq f '+) =>  +
(apply f '(1 2)) =>  3
(setq f #'-) =>  #<FUNCTION ->
(apply f '(1 2)) =>  -1
(apply #'max 3 5 '(2 7 3)) =>  7
(apply 'cons '((+ 2 3) 4)) =>  ((+ 2 3) . 4)
(apply #'+ '()) =>  0

(defparameter *some-list* '(a b c))
(defun strange-test (&rest x) (eq x *some-list*))
(apply #'strange-test *some-list*) =>  implementation-dependent

(defun bad-boy (&rest x) (rplacd x 'y))
(bad-boy 'a 'b 'c) has undefined consequences.
(apply #'bad-boy *some-list*) has undefined consequences.
(defun foo (size &rest keys &key double &allow-other-keys)
  (let ((v (apply #'make-array size :allow-other-keys t keys)))
    (if double (concatenate (type-of v) v v) v)))
(foo 4 :initial-contents '(a b c d) :double t)
   =>  #(A B C D A B C D)
```


## 影響

なし。


## 例外

なし。


## 参考

[`funcall`](5.3.funcall.html),
[`fdefinition`](5.3.fdefinition.html),
[`function`](5.3.function-special.html),
3.1. 評価,
5.1.2.5. APPLYの`place`フォーム


## 備考

なし。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

