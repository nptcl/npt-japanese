% Macro WITH-SIMPLE-RESTART

[UP](9.2.html)  

---

# Macro **WITH-SIMPLE-RESTART**


Macro `WITH-SIMPLE-RESTART`


## 構文

`with-simple-restart` (*name* *format-control* *format-argument\**) *form\**  
=> result*


## 引数と戻り値

*name* - シンボル  
*format-control* - format-control  
*format-argument* - オブジェクト（これはformat-arguments）  
*form* - 暗黙のprogn  
*result* - 通常の状況では*form*による返却値であり、
*name*という`restart`が起動されたことによる例外的な状況では、
2つの値[`nil`](5.3.nil-variable.html)と[`t`](5.3.t-variable.html)が返却されます。


## 定義

[`with-simple-restart`](9.2.with-simple-restart.html)は`restart`を確立します。

もし*name*で指定された`restart`が*form*実行中に起動されなかったら、
*form*の最後から返却される全ての値が返却されます。
もし*name*で指定された`restart`が*form*実行中に起動されたら、
制御は[`with-simple-restart`](9.2.with-simple-restart.html)へ遷移し、
2つの値[`nil`](5.3.nil-variable.html)と[`t`](5.3.t-variable.html)が返却されます。

もし*name*が[`nil`](5.3.nil-variable.html)のとき、
匿名の`restart`が確立されます。

*format-control*と*format-argument*は、
`restart`の報告のときに使用されます。


## 例文

```lisp
(defun read-eval-print-loop (level)
  (with-simple-restart (abort "Exit command level ~D." level)
    (loop
      (with-simple-restart (abort "Return to command level ~D." level)
        (let ((form (prog2 (fresh-line) (read) (fresh-line))))
          (prin1 (eval form)))))))
=>  READ-EVAL-PRINT-LOOP
(read-eval-print-loop 1)
(+ 'a 3)
>>  Error: The argument, A, to the function + was of the wrong type.
>>         The function expected a number.
>>  To continue, type :CONTINUE followed by an option number:
>>   1: Specify a value to use this time.
>>   2: Return to command level 1.
>>   3: Exit command level 1.
>>   4: Return to Lisp Toplevel.
(defun compute-fixnum-power-of-2 (x)
  (with-simple-restart (nil "Give up on computing 2^~D." x)
    (let ((result 1))
      (dotimes (i x result)
        (setq result (* 2 result))
        (unless (fixnump result)
          (error "Power of 2 is too large."))))))
COMPUTE-FIXNUM-POWER-OF-2
(defun compute-power-of-2 (x)
  (or (compute-fixnum-power-of-2 x) 'something big))
COMPUTE-POWER-OF-2
(compute-power-of-2 10)
1024
(compute-power-of-2 10000)
>>  Error: Power of 2 is too large.
>>  To continue, type :CONTINUE followed by an option number.
>>   1: Give up on computing 2^10000.
>>   2: Return to Lisp Toplevel
>>  Debug> :continue 1
=>  SOMETHING-BIG
```


## 副作用

なし。


## 影響

なし。


## 例外

なし。


## 参考

[`restart-case`](9.2.restart-case.html)


## 備考

[`with-simple-restart`](9.2.with-simple-restart.html)は、
もっとも一般的に使用される[`restart-case`](9.2.restart-case.html)の
短縮形のひとつです。

[`with-simple-restart`](9.2.with-simple-restart.html)は次のように定義できます。

```lisp
(defmacro with-simple-restart ((restart-name format-control
                                             &rest format-arguments)
                               &body forms)
  `(restart-case (progn ,@forms)
     (,restart-name ()
         :report (lambda (stream)
                   (format stream ,format-control ,@format-arguments))
        (values nil t))))
```

例外的な状況では第二返却値が[`t`](5.3.t-variable.html)になるため、
通常の場合は第二返却値を省略するか[`nil`](5.3.nil-variable.html)にして、
2つの状況を区別できるようにするのが一般的です（ただし必須ではありません）。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

