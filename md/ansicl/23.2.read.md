% Function READ, READ-PRESERVING-WHITESPACE

[UP](23.2.html)  

---

# Function **READ, READ-PRESERVING-WHITESPACE**


Function `READ`, `READ-PRESERVING-WHITESPACE`


## 構文

`read`
 `&optional` *input-stream* *eof-error-p* *eof-value* *recursive-p*
 => *object*  
`read-preserving-whitespace`
 `&optional` *input-stream* *eof-error-p* *eof-value* *recursive-p*
 => *object*


## 引数と戻り値

*input-stream* - 入力ストリームの指定子  
*eof-error-p* - generalized-boolean。デフォルトは*true*。  
*eof-value* - オブジェクト。デフォルトは[`nil`](5.3.nil-variable.html)。  
*recursive-p* - generalized-boolean。デフォルトは*false*。  
*object* - オブジェクト（Lispリーダーによって読み込まれたもの）か、*eof-value*


## 定義

[`read`](23.2.read.html)は、*input-stream*から
オブジェクトの印刷表現を構文解析し、
そのようなオブジェクトを構築します。
[`read-preserving-whitespace`](23.2.read.html)は[`read`](23.2.read.html)と似ていますが、
印刷表現されたオブジェクトを区切っている
どのような空白文字も保護します。
[`read-preserving-whitespace`](23.2.read.html)は、
[`read-preserving-whitespace`](23.2.read.html)自身の引数*recursive-p*が*true*のときは、
正確に[`read`](23.2.read.html)のように動作します。

[`*read-suppress*`](23.2.read-suppress.html)が*false*のとき、
[`read`](23.2.read.html)は印刷表現として要求されている特定の区切り文字である
空白文字のときはそれを捨てます。
しかし、[`read`](23.2.read.html)は
それが構文として意味を持つ場合は、
次の式の開始に使用できるようにするために、
その空白文字を保護します（[`unread-char`](21.2.unread-char.html)で使用されます）。

もしシンボルか数のあとにすぐファイルの終わりが続いたとき、
[`read`](23.2.read.html)はそのシンボルか数の読み込みは成功します。
そして再び呼び出されたときにファイルの終わりが発生し、
ただ*eof-error-p*に従い動作します。
もしファイルの終端に例えば空行やコメントなどの
無視できるテキストが含まれていたとき、
[`read`](23.2.read.html)はそれをオブジェクトの途中で終了したとはみなしません。

もし*recursive-p*が*true*の場合、
[`read`](23.2.read.html)の呼び出しは
トップレベルからのものではなく、
それ自身が[`read`](23.2.read.html)から呼び出されるような何らかの内部関数であるか、
あるいは似たような入力関数で構成されることが期待されます。

両関数は*input-stream*から読み込んだオブジェクトを返却します。
もし*eof-error-p*が*false*であり、
オブジェクトの開始より前にファイルの終わりに到達したときは、
*eof-value*が返却されます。


## 例文

```lisp
(read)
>>  'a
=>  (QUOTE A)
(with-input-from-string (is " ") (read is nil 'the-end)) =>  THE-END
(defun skip-then-read-char (s c n)
   (if (char= c #\{) (read s t nil t) (read-preserving-whitespace s))
   (read-char-no-hang s)) =>  SKIP-THEN-READ-CHAR
(let ((*readtable* (copy-readtable nil)))
   (set-dispatch-macro-character #\# #\{ #'skip-then-read-char)
   (set-dispatch-macro-character #\# #\} #'skip-then-read-char)
   (with-input-from-string (is "#{123 x #}123 y")
     (format t "~S ~S" (read is) (read is)))) =>  #\x, #\Space, NIL
```

例として、次のリーダーマクロの定義を考えます。

```lisp
(defun slash-reader (stream char)
  (declare (ignore char))
  `(path . ,(loop for dir = (read-preserving-whitespace stream t nil t)
                  then (progn (read-char stream t nil t)
                              (read-preserving-whitespace stream t nil t))
                  collect dir
                  while (eql (peek-char nil stream nil nil t) #\/))))
(set-macro-character #\/ #'slash-reader)
```

ここで、次の式に対して[`read`](23.2.read.html)を呼び出すことを考えます。

```lisp
(zyedh /usr/games/zork /usr/games/boggle)
```

マクロ`/`は、複数の`/`文字によって分割されたオブジェクトを読み込むので、
`/usr/games/zork`は、`(path usr games zork)`のように読み込まれます。
したがって例の全体の式は、次のように読み込まれます。


```lisp
(zyedh (path usr games zork) (path usr games boggle))
```

しかし、もし[`read`](23.2.read.html)が
[`read-preserving-whitespace`](23.2.read.html)の代わりに使用されたとき、
シンボル`zork`を読み込んだ後に続く空白は切り捨てられます。
次に[`peek-char`](21.2.peek-char.html)が呼ばれたときは続く`/`を見るので、
繰り返しは継続し、次のように解釈されます。

```lisp
(zyedh (path usr games zork usr games boggle))
```

これは空白が切り捨てられたときのものです。
もしインタープリターのコマンドが単一の文字のコマンドを受け取り
さらなるオブジェクトの読み込みがあるとき、
シンボルの後の空白文字を切り捨てないときは、
そのシンボルが読み込まれた後にコマンドが解釈されるかもしれません。


## 影響

[`*standard-input*`](21.2.debug-io.html),
[`*terminal-io*`](21.2.terminal-io.html),
[`*readtable*`](23.2.readtable-variable.html),
[`*read-default-float-format*`](23.2.read-default-float-format.html),
[`*read-base*`](23.2.read-base.html),
[`*read-suppress*`](23.2.read-suppress.html),
[`*package*`](11.2.package-variable.html),
[`*read-eval*`](23.2.read-eval.html)


## 例外

[`read`](23.2.read.html)は、オブジェクトの表現の最中にファイルが終わったときは、
*eof-error-p*に関係なく型[`end-of-file`](21.2.end-of-file.html)のエラーが通知されます。
例えば、あるファイルから読み込まれるオブジェクト内ににおいて、
左かっこに対応するはずの十分な右かっこが含まれていなかったときは、
[`read`](23.2.read.html)はエラーを通知します。
これは、
[`read`](23.2.read.html)か[`read-preserving-whitespace`](23.2.read.html)が呼び出されるとき、
*recursive-p*と*eof-error-p*が[`nil`](5.3.nil-variable.html)ではなく、
あるオブジェクトの開始前にファイルの終わりに到達したときに
検出されます。

*eof-error-p*が*true*のとき、
ファイルの終わりにて型[`end-of-file`](21.2.end-of-file.html)のエラーが通知されます。


## 参考

[`peek-char`](21.2.peek-char.html),
[`read-char`](21.2.read-char.html),
[`unread-char`](21.2.unread-char.html),
[`read-from-string`](23.2.read-from-string.html),
[`read-delimited-list`](23.2.read-delimited-list.html),
[`parse-integer`](12.2.parse-integer.html),
[2. 構文](2.html),
[23.1. リーダーの説明](23.1.html)


## 備考

なし。


---
[TOP](index.html),  [Github](https://github.com/nptcl/npt-japanese)

